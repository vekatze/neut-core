import {
  this.bool,
  this.box,
  this.c-int {eq-c-int},
  this.c-size {C-Size, add-c-size, c-size, mul-c-size},
  this.external {free, malloc, thread-create, thread-exit, thread-join},
  this.foreign {THREAD_SIZE},
  this.magic {cast},
  this.memory {load-int, load-int1, load-pointer, store-int, store-int1},
  this.pointer {shift},
  this.unit {Unit, unit},
  this.word,
}

inline _embody(a: type, x: &a): a {
  *x
}

inline _thread-size(): c-size {
  let u = this.word.word-size in
  add-c-size(mul-c-size(u, C-Size(3)), C-Size(1))
}

nominal {
  _await-and-write-result(addr: pointer): unit,
  thread(_: type): type,
  attach(a: type, e: thread(a)): a,
}

resource _thread-inner {
  function (addr: pointer) {
    let u = this.word.size in
    let elem-type = cast(int, type, load-int(shift(addr, u))) in
    let pr: thread(elem-type) = cast(pointer, thread(elem-type), addr) in
    let _: elem-type = this.thread.attach(elem-type, pr) in
    Unit
  },
  function (addr: pointer) {
    let u = this.word.size in
    let u2 = mul-int(u, 2) in
    let u3 = mul-int(u, 3) in
    _await-and-write-result(addr);
    let thread-id = load-int(addr) in
    let elem-type = cast(int, type, load-int(shift(addr, u))) in
    let elem-value: &elem-type = cast(int, &elem-type, load-int(shift(addr, u2))) in
    let new-addr = malloc(_thread-size()) in
    store-int(thread-id, new-addr);
    store-int(cast(type, int, elem-type), shift(new-addr, u));
    let cloned-value: elem-type = _embody(elem-type, elem-value) in
    store-int(cast(elem-type, int, cloned-value), shift(new-addr, u2));
    store-int1(1, shift(new-addr, u3));
    new-addr
  },
}

// `thread` must be defined by `define` (and not by `data`) so that
// `thread(a)` is consider to be possibly noetic
define thread(_: type): type {
  _thread-inner
}

define attach(a: type, e: thread(a)): a {
  let addr = cast(thread(a), pointer, e) in
  _await-and-write-result(addr);
  let u = this.word.size in
  let value-ptr = shift(addr, mul-int(u, 2)) in
  let value = load-int(value-ptr) in
  free(addr);
  cast(int, a, value)
}

define _await-and-write-result(addr: pointer): unit {
  let u = this.word.size in
  let u2 = mul-int(u, 2) in
  let u3 = mul-int(u, 3) in
  let finished-flag-ptr = shift(addr, u3) in
  let finished-flag = load-int1(finished-flag-ptr) in
  if eq-int1(finished-flag, 0) {
    let thread-id = load-pointer(addr) in
    let value-ptr = shift(addr, u2) in
    thread-join(thread-id, value-ptr);
    store-int1(1, finished-flag-ptr)
  } else {
    Unit
  }
}

// (thread-id, result-type, result-value, finished-flag): thread(result-type)
define detach(a: type, f: () -> a): thread(a) {
  let thread-func =
    function () {
      thread-exit(cast(a, pointer, f()))
    }
  in
  let u = this.word.size in
  let u2 = mul-int(u, 2) in
  let u3 = mul-int(u, 3) in
  let closure = cast(() -> unit, pointer, thread-func) in
  let closure-env = load-pointer(shift(closure, u)) in
  let closure-label = load-pointer(shift(closure, u2)) in
  free(closure);
  let thread-id-pointer = malloc(THREAD_SIZE) in
  let thread-create =
    define naive-loop(): unit {
      let result = thread-create(thread-id-pointer, closure-label, closure-env) in
      if eq-c-int(result, 0) {
        Unit
      } else {
        naive-loop()
      }
    }
  in
  let _ = thread-create() in
  let thread-id = load-int(thread-id-pointer) in
  free(thread-id-pointer);
  let ptr = malloc(_thread-size()) in
  store-int(thread-id, ptr);
  store-int(cast(type, int, a), shift(ptr, u));
  store-int(0, shift(ptr, u2));
  store-int1(0, shift(ptr, u3));
  cast(pointer, thread(a), ptr)
}
