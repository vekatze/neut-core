import {
  this.bool {bool},
  this.c-int {C-Int, c-int, eq-c-int},
  this.c-string {c-string, from-c-string, to-c-string},
  this.control {discard},
  this.either {Right, none, right-unit},
  this.list {Cons, list},
  this.magic,
  this.memory {load-int, load-pointer},
  this.pointer {shift},
  this.system {report-system-error, system},
  this.text {_get-content-pointer, text},
  this.unit {Unit, unit},
  this.word,
}

foreign {
  getenv(pointer): pointer,
  setenv(pointer, pointer, c-int): c-int,
}

define get-env<r := right-unit>(name: &text): ?text {
  let c-str = _get-content-pointer(name);
  let tmp = magic external getenv(c-str);
  if eq-int(magic cast(pointer, int, tmp), 0) {
    try _ = r;
    none
  } else {
    try _ = r;
    Right(from-c-string(this.magic.cast(pointer, &c-string, tmp)))
  }
}

define set-env<r := right-unit>(name: &text, value: &text, overwrite: bool): system(unit) {
  let name-c-str = to-c-string(name);
  let value-c-str = to-c-string(value);
  let flag =
    if overwrite {
      1
    } else {
      0
    };
  let result on name-c-str, value-c-str =
    magic external setenv(
      magic cast(&c-string, pointer, name-c-str),
      magic cast(&c-string, pointer, value-c-str),
      C-Int(flag),
    );
  discard(name-c-str);
  discard(value-c-str);
  if eq-c-int(result, 0) {
    try _ = r;
    Right(Unit)
  } else {
    try _ = r;
    report-system-error()
  }
}

define get-argc(): int {
  let argc-ptr = magic global("neut-unsafe-argc", pointer);
  load-int(argc-ptr)
}

define get-argv(): list(text) {
  let argc = get-argc();
  let argv-ptr = magic global("neut-unsafe-argv", pointer);
  let argv = load-pointer(argv-ptr);
  let u = this.word.size;
  let f =
    define get-argv-helper(i: int): list(text) {
      if eq-int(i, argc) {
        []
      } else {
        let rest = get-argv-helper(add-int(i, 1));
        let str-ptr-ptr = shift(argv, mul-int(i, u));
        let str-ptr = load-int(str-ptr-ptr);
        let t = from-c-string(this.magic.cast(int, &c-string, str-ptr));
        Cons(t, rest)
      }
    };
  f(0)
}
