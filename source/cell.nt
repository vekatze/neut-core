import {
  this.box,
  this.c-size {C-Size, from-c-size},
  this.external {
    free,
    malloc,
    thread-cond-destroy,
    thread-cond-init,
    thread-cond-signal,
    thread-mutex-destroy,
    thread-mutex-init,
    thread-mutex-lock,
    thread-mutex-unlock,
  },
  this.foreign {THREAD_COND_SIZE, THREAD_MUTEX_SIZE},
  this.int {sum-int},
  this.list,
  this.magic {cast},
  this.memory {load-int, store-int},
  this.pair {Pair, pair},
  this.pointer {shift},
  this.unit {Unit, unit},
  this.word,
}

inline _get-clone(a: type, x: &a): a {
  *x
}

nominal {
  _cell: type,
}

data cell(_: type) {
| _Cell(_cell)
}

define _new(a: type, val: a): cell(a) {
  let mutex-size = from-c-size(THREAD_MUTEX_SIZE) in
  let cond-size = from-c-size(THREAD_COND_SIZE) in
  let u = this.word.size in
  let ptr = malloc(C-Size(sum-int([u, mutex-size, cond-size, u]))) in
  store-int(cast(a, int, val), ptr);
  let _ = thread-mutex-init(shift(ptr, u)) in
  thread-cond-init(shift(ptr, add-int(u, mutex-size)));
  store-int(cast(type, int, a), shift(ptr, add-int(u, add-int(mutex-size, cond-size))));
  cast(pointer, cell(a), ptr)
}

// (content, thread-mutex, thread-cond, type)
resource _cell {
  function (ch: pointer) {
    let u = this.word.size in
    let mutex-size = from-c-size(THREAD_MUTEX_SIZE) in
    let cond-size = from-c-size(THREAD_COND_SIZE) in
    let cond-offset = add-int(u, mutex-size) in
    let type-offset = add-int(u, add-int(mutex-size, cond-size)) in
    let a = cast(int, type, load-int(shift(ch, type-offset))) in
    thread-cond-destroy(shift(ch, cond-offset));
    thread-mutex-destroy(shift(ch, u));
    let _: a = magic cast(int, a, load-int(ch)) in
    free(ch)
  },
  function (ch: pointer) {
    let u = this.word.size in
    let mutex-size = from-c-size(THREAD_MUTEX_SIZE) in
    let cond-size = from-c-size(THREAD_COND_SIZE) in
    let type-offset = add-int(u, add-int(mutex-size, cond-size)) in
    let a = cast(int, type, load-int(shift(ch, type-offset))) in
    let val: &a = cast(int, &a, load-int(ch)) in
    let val-clone: a = _get-clone(a, val) in
    cast(cell(a), pointer, _new(a, val-clone))
  },
}

inline _new-cell<a>(initial-value: a): cell(a) {
  _new(a, initial-value)
}

inline modify<a, b>(ch: &cell(a), f: (a) -> pair(a, b)): b {
  let ch = cast(&cell(a), pointer, ch) in
  let u = this.word.size in
  let mutex-ptr = shift(ch, u) in
  thread-mutex-lock(mutex-ptr);
  let val-base: int = load-int(ch) in
  let orig-val = cast(int, a, val-base) in
  let Pair(new-val, result) = f(orig-val) in
  store-int(cast(a, int, new-val), ch);
  let mutex-size = from-c-size(THREAD_MUTEX_SIZE) in
  thread-cond-signal(shift(ch, add-int(u, mutex-size)));
  thread-mutex-unlock(mutex-ptr);
  result
}

inline mutate<a>(ch: &cell(a), f: (a) -> a): unit {
  modify(ch, function (x) {
    Pair(f(x), Unit)
  })
}

inline borrow<a, b>(ch: &cell(a), f: (&a) -> meta b): b {
  let ch = cast(&cell(a), pointer, ch) in
  let u = this.word.size in
  let mutex-ptr = shift(ch, u) in
  thread-mutex-lock(mutex-ptr);
  let val-base: int = load-int(ch) in
  let val: &a = cast(int, &a, val-base) in
  letbox-T result = f(val) in
  let mutex-size = from-c-size(THREAD_MUTEX_SIZE) in
  thread-cond-signal(shift(ch, add-int(u, mutex-size)));
  thread-mutex-unlock(mutex-ptr);
  result
}

define clone<a>(ch: &cell(a)): a {
  borrow(ch, function (val: &a) {
    box val {val}
  })
}

define extract<a>(c: cell(a)): a {
  let u = this.word.size in
  let ch = cast(cell(a), pointer, c) in
  let result = cast(int, a, load-int(ch)) in
  thread-mutex-destroy(shift(ch, u));
  thread-cond-destroy(shift(ch, add-int(u, from-c-size(THREAD_MUTEX_SIZE))));
  this.external.free(ch);
  result
}
