import {
  this.bool {and, bool, or},
  this.box,
  this.either {Left, Right, either},
  this.int.parse {parse-hex},
  this.list {Cons, List, Nil, concat, for-each-E, for-each-EN, intercalate, list},
  this.pair {Pair},
  this.rune,
  this.rune.eq {eq-rune},
  this.rune.ord {le-rune},
  this.text {
    drop,
    format,
    length,
    singleton,
    split,
    take-while-N,
    text,
  },
}

define _is-lower-hex-digit(r: rune): bool {
  fold-left<or>[
    and(le-rune(`0`, r), le-rune(r, `9`)),
    and(le-rune(`a`, r), le-rune(r, `f`)),
  ]
}

data error {
| Parse-Error(text)
}

define _read-char(r: rune, t: text): either(error, text) {
  match this.text.uncons(t) {
  | Right(Pair(r', rest)) =>
    if eq-rune(r, r') {
      Right(rest)
    } else {
      Left(Parse-Error(format("Expected `{}`, but got: `{}`", List[singleton(r), singleton(r')])))
    }
  | Left(_) =>
    Left(Parse-Error(format("Expected `{}`, but got: end of text", List[singleton(r)])))
  }
}

define _read-unicode-scalar-value(t: &text): either(error, rune) {
  match parse-hex(t) {
  | Left(_) =>
    Left(Parse-Error(format("Could not parse `{}` as a hexadecimal integer", List[*t])))
  | Right(v) =>
    match this.rune.from-int(v) {
    | Right(r) =>
      Right(r)
    | Left(_) =>
      Left(Parse-Error(format("The value `{}` is outside the Unicode codespace", List[*t])))
    }
  }
}

define _unquote-text(t: text): either(error, list(text)) {
  match this.text.uncons(t) {
  | Left(_) =>
    Right(List[*"\\"])
  | Right(Pair(c, rest)) =>
    if eq-rune(c, `0`) {
      Right(List[*"\u{0}", rest])
    } else-if eq-rune(c, `t`) {
      Right(List[*"\u{9}", rest])
    } else-if eq-rune(c, `n`) {
      Right(List[*"\u{a}", rest])
    } else-if eq-rune(c, `r`) {
      Right(List[*"\u{d}", rest])
    } else-if eq-rune(c, `"`) {
      Right(List[*"\u{22}", rest])
    } else-if eq-rune(c, `\\`) {
      Right(List[*"\u{5c}", rest])
    } else-if eq-rune(c, `\``) {
      Right(List[*"\u{60}", rest])
    } else-if eq-rune(c, `u`) {
      try rest' = _read-char(`{`, rest);
      let hex-text on rest' = take-while-N(rest', _is-lower-hex-digit);
      pin hex-text = hex-text;
      let scalar-value-text-len = length(hex-text);
      let rest'' = drop(rest', scalar-value-text-len);
      try rest''' = _read-char(`}`, rest'');
      try r = _read-unicode-scalar-value(hex-text);
      Right(List[singleton(r), rest'''])
    } else {
      Left(Parse-Error(format("Unknown escape sequence: \\{}", List[singleton(c)])))
    }
  }
}

define _parse-text-fragment(t: &text): either(error, list(text)) {
  match split(t, "\\") {
  | Nil =>
    // unreachable
    Left(Parse-Error(*"(core).text.parse._parse-text-fragment"))
  | Cons(t', ts') =>
    try fragment-lists = for-each-E(ts', _unquote-text);
    Right(Cons(t', concat(fragment-lists)))
  }
}

define parse-text(t: &text): either(error, text) {
  pin ts = split(t, "\\\\");
  try tss = for-each-EN(ts, _parse-text-fragment);
  Right(this.text.join(this.list.concat(intercalate(tss, List[*"\\"]))))
}
