import {
  this.bool {False, True, and, bool, not, or},
  this.box,
  this.c-size {C-Size},
  this.control {grow, loop},
  this.either {Left, Right, either, none, rho},
  this.external {free, malloc, realloc},
  this.function {compose},
  this.int {abs-int, max-int, min-int},
  this.int.io {print-int-line},
  this.list {Cons, Nil, list},
  this.magic {cast},
  this.memory {load-int, load-pointer, store-int, store-pointer},
  this.pair {Pair, pair},
  this.pointer {shift},
  this.text {replicate, text},
  this.text.io {print, print-line},
  this.trick,
  this.type-tag {Vector, type-tag-to-int},
  this.unit {Unit, unit},
  this.word,
}

nominal {
  _vector-internal: type,
}

// (capacity: int, from: int, to: int, elem-type: type, ptr-to-buffer: pointer)
data vector(_: type) {
| _Vector(_vector-internal)
}

inline get-capacity<a>(xs: &vector(a)): int {
  let vector-ptr = cast(&vector(a), pointer, xs);
  load-int(vector-ptr)
}

inline _get-from<a>(xs: &vector(a)): int {
  let vector-ptr = cast(&vector(a), pointer, xs);
  load-int(shift(vector-ptr, this.word.size))
}

inline _get-to<a>(xs: &vector(a)): int {
  let vector-ptr = cast(&vector(a), pointer, xs);
  load-int(shift(vector-ptr, mul-int(this.word.size, 2)))
}

inline _get-elem-type<a>(xs: &vector(a)): type {
  let vector-ptr = cast(&vector(a), pointer, xs);
  cast(int, type, load-int(shift(vector-ptr, mul-int(this.word.size, 3))))
}

inline _set-elem-type<a>(xs: &vector(a), b: type): unit {
  let vector-ptr = cast(&vector(a), pointer, xs);
  store-int(cast(type, int, b), shift(vector-ptr, mul-int(this.word.size, 3)))
}

inline _get-buffer-ptr<a>(xs: &vector(a)): pointer {
  let vector-ptr = cast(&vector(a), pointer, xs);
  load-pointer(shift(vector-ptr, mul-int(this.word.size, 4)))
}

define _set-buffer-ptr<a>(xs: &vector(a), buffer-ptr: pointer): unit {
  let vector-ptr = cast(&vector(a), pointer, xs);
  store-pointer(buffer-ptr, shift(vector-ptr, mul-int(this.word.size, 4)))
}

define _set-capacity<a>(xs: &vector(a), capacity: int): unit {
  let vector-ptr = cast(&vector(a), pointer, xs);
  store-int(capacity, vector-ptr)
}

define _get-element-pointer<a>(xs: &vector(a), index: int): pointer {
  let from = _get-from(xs);
  let buffer-ptr = _get-buffer-ptr(xs);
  shift(buffer-ptr, mul-int(this.word.size, add-int(from, index)))
}

define length<a>(xs: &vector(a)): int {
  let from = _get-from(xs);
  let to = _get-to(xs);
  sub-int(to, from)
}

inline _shift-from<a>(xs: &vector(a), offset: int): unit {
  let vector-ptr = cast(&vector(a), pointer, xs);
  let from-ptr = shift(vector-ptr, mul-int(this.word.size, 1));
  let current-value = load-int(from-ptr);
  store-int(add-int(current-value, offset), from-ptr)
}

inline _shift-to<a>(xs: &vector(a), offset: int): unit {
  let vector-ptr = cast(&vector(a), pointer, xs);
  let to-ptr = shift(vector-ptr, mul-int(this.word.size, 2));
  let current-value = load-int(to-ptr);
  store-int(add-int(current-value, offset), to-ptr)
}

inline _set-from<a>(xs: &vector(a), from: int): unit {
  let vector-ptr = cast(&vector(a), pointer, xs);
  store-int(from, shift(vector-ptr, this.word.size))
}

inline _set-to<a>(xs: &vector(a), to: int): unit {
  let vector-ptr = cast(&vector(a), pointer, xs);
  store-int(to, shift(vector-ptr, mul-int(this.word.size, 2)))
}

define make<a>(initial-capacity: int): vector(a) {
  let abs-capacity = abs-int(initial-capacity);
  let word-size = this.word.size;
  let vector-ptr = malloc(C-Size(mul-int(word-size, 5)));
  let buffer-ptr = malloc(C-Size(mul-int(word-size, abs-capacity)));
  let initial-cursor = match gt-int(initial-capacity, 0) {True => 0 | False => abs-capacity};
  store-int(abs-capacity, vector-ptr);
  store-int(initial-cursor, shift(vector-ptr, mul-int(word-size, 1)));
  store-int(initial-cursor, shift(vector-ptr, mul-int(word-size, 2)));
  store-int(cast(type, int, a), shift(vector-ptr, mul-int(word-size, 3)));
  store-pointer(buffer-ptr, shift(vector-ptr, mul-int(word-size, 4)));
  magic cast(pointer, vector(a), vector-ptr)
}

define _discard-vector-content<a>(xs: &vector(a), elem-type: type, index: int, len: int): unit {
  if eq-int(index, len) {
    Unit
  } else {
    let element-ptr = _get-element-pointer(xs, index);
    let _ = cast(int, elem-type, load-int(element-ptr));
    _discard-vector-content(xs, elem-type, add-int(index, 1), len)
  }
}

define _copy-vector-content(
  xs: &vector(pointer),
  ys: &vector(pointer),
  elem-type: type,
  index: int,
  len: int,
): unit {
  if eq-int(index, len) {
    Unit
  } else {
    let source-ptr = _get-element-pointer(xs, index);
    let target-ptr = _get-element-pointer(ys, index);
    let original = cast(int, &elem-type, load-int(source-ptr));
    let clone = *original;
    store-int(cast(elem-type, int, clone), target-ptr);
    _copy-vector-content(xs, ys, elem-type, add-int(index, 1), len)
  }
}

define _free-vector<a>(xs: &vector(a)): unit {
  let xs = cast(&vector(a), &vector(pointer), xs);
  free(_get-buffer-ptr(xs));
  free(cast(&vector(pointer), pointer, xs));
}

// (capacity: int, from: int, to: int, elem-type: type, ptr-to-buffer: pointer)
resource _vector-internal {
  function (xs: &vector(pointer)) {
    let len = length(xs);
    let elem-type = _get-elem-type(xs);
    _discard-vector-content(xs, elem-type, 0, len);
    _free-vector(xs);
  },
  function (xs: &vector(pointer)) {
    let capacity = get-capacity(xs);
    let ys: &vector(pointer) = cast(vector(pointer), &vector(pointer), make(capacity));
    let elem-type = _get-elem-type(xs);
    let from = _get-from(xs);
    let to = _get-to(xs);
    let len = length(xs);
    _set-from(ys, from);
    _set-to(ys, to);
    _set-elem-type(ys, elem-type);
    _copy-vector-content(xs, ys, elem-type, 0, len);
    ys
  },
  type-tag-to-int(Vector),
}

foreign {
  llvm.memmove.p0.p0.i64(pointer, pointer, int64, int1): void,
  llvm.memcpy.p0.p0.i64(pointer, pointer, int64, int1): void,
}

inline _extend-factor: int {
  2
}

define _extend-front<a>(xs: vector(a)): vector(a) {
  let current-capacity on xs = max-int(get-capacity(xs), 1);
  let new-capacity = mul-int(current-capacity, _extend-factor);
  let buffer-ptr on xs = _get-buffer-ptr(xs);
  let new-buffer-ptr = realloc(buffer-ptr, C-Size(mul-int(this.word.size, new-capacity)));
  let new-head-ptr = shift(new-buffer-ptr, mul-int(this.word.size, current-capacity));
  let _ =
    magic external llvm.memcpy.p0.p0.i64(
      new-head-ptr,
      new-buffer-ptr,
      mul-int(this.word.size, current-capacity),
      0,
    );
  let _ on xs = {
    _shift-from(xs, current-capacity);
    _shift-to(xs, current-capacity);
    _set-buffer-ptr(xs, new-buffer-ptr);
    _set-capacity(xs, new-capacity)
  };
  xs
}

define _extend-back<a>(xs: vector(a)): vector(a) {
  let current-capacity on xs = max-int(get-capacity(xs), 1);
  let new-capacity = mul-int(current-capacity, _extend-factor);
  let buffer-ptr on xs = _get-buffer-ptr(xs);
  let new-buffer-ptr = realloc(buffer-ptr, C-Size(mul-int(this.word.size, new-capacity)));
  let _ on xs = {
    _set-buffer-ptr(xs, new-buffer-ptr);
    _set-capacity(xs, new-capacity)
  };
  xs
}

define push-front<a>(xs: vector(a), x: a): vector(a) {
  let from on xs = _get-from(xs);
  if gt-int(from, 0) {
    let new-element-ptr on xs = _get-element-pointer(xs, -1);
    store-int(cast(a, int, x), new-element-ptr);
    let _ on xs = _shift-from(xs, -1);
    xs
  } else {
    let xs = _extend-front(xs);
    push-front(xs, x)
  }
}

define push-back<a>(xs: vector(a), x: a): vector(a) {
  let capacity on xs = get-capacity(xs);
  let to on xs = _get-to(xs);
  if lt-int(to, capacity) {
    let len on xs = length(xs);
    let new-element-ptr on xs = _get-element-pointer(xs, len);
    store-int(cast(a, int, x), new-element-ptr);
    let _ on xs = _shift-to(xs, 1);
    xs
  } else {
    let xs' = _extend-back(xs);
    push-back(xs', x)
  }
}

define pop-front<a, r := rho>(xs: vector(a)): ?pair(a, vector(a)) {
  let len on xs = length(xs);
  if eq-int(len, 0) {
    try _ = r;
    none
  } else {
    let first-value-pointer on xs = _get-element-pointer(xs, 0);
    let first-value = cast(int, a, load-int(first-value-pointer));
    let _ on xs = _shift-from(xs, 1);
    try _ = r;
    Right(Pair(first-value, xs))
  }
}

define pop-back<a, r := rho>(xs: vector(a)): ?pair(vector(a), a) {
  let len on xs = length(xs);
  if eq-int(len, 0) {
    try _ = r;
    none
  } else {
    let last-value-pointer on xs = _get-element-pointer(xs, sub-int(len, 1));
    let last-value = cast(int, a, load-int(last-value-pointer));
    let _ on xs = _shift-to(xs, -1);
    try _ = r;
    Right(Pair(xs, last-value))
  }
}

define singleton<a>(x: a): vector(a) {
  let v = make(1);
  push-back(v, x)
}

inline _access<a>(xs: &vector(a), i: int): &a {
  let element-pointer = _get-element-pointer(xs, i);
  cast(int, &a, load-int(element-pointer))
}

define access<a, r := rho>(xs: &vector(a), i: int): ?&a {
  if and(le-int(0, i), lt-int(i, length(xs))) {
    try _ = r;
    Right(_access(xs, i))
  } else {
    try _ = r;
    none
  }
}

inline fold-left<elem-type, result-type>(
  acc: result-type,
  xs: vector(elem-type),
  !consumer: (result-type, elem-type, int) -> result-type,
): result-type {
  let sz on xs = length(xs);
  let helper =
    define loop(cursor: int, acc: result-type, xs: vector(elem-type)): result-type {
      if eq-int(cursor, sz) {
        let xs = cast(vector(elem-type), &vector(pointer), xs);
        _free-vector(xs);
        acc
      } else {
        let element-ptr on xs = _get-element-pointer(xs, cursor);
        let old-value = cast(int, elem-type, load-int(element-ptr));
        let new-value = !consumer(acc, old-value, cursor);
        loop(add-int(cursor, 1), new-value, xs)
      }
    };
  helper(0, acc, xs)
}

inline fold-left-N<elem-type, result-type>(
  acc: result-type,
  xs: &vector(elem-type),
  !consumer: (result-type, &elem-type, int) -> result-type,
): result-type {
  let sz = length(xs);
  let helper =
    define loop(cursor: int, acc, xs) {
      if eq-int(cursor, sz) {
        acc
      } else {
        let y = _access(xs, cursor);
        loop(add-int(cursor, 1), !consumer(acc, y, cursor), xs)
      }
    };
  helper(0, acc, xs)
}

inline fold-left-reverse-N<elem-type, result-type>(
  acc: result-type,
  xs: &vector(elem-type),
  !consumer: (result-type, &elem-type, int) -> result-type,
): result-type {
  let sz = length(xs);
  let helper =
    define loop(cursor: int, acc: result-type, xs: &vector(elem-type)): result-type {
      if lt-int(cursor, 0) {
        acc
      } else {
        let y = this.vector._access(xs, cursor);
        loop(sub-int(cursor, 1), !consumer(acc, y, cursor), xs)
      }
    };
  helper(sub-int(sz, 1), acc, xs)
}

inline fold-left-EN<e, elem-type, result-type>(
  acc: result-type,
  xs: &vector(elem-type),
  !consumer: (result-type, &elem-type, int) -> either(e, result-type),
): either(e, result-type) {
  let sz = length(xs);
  let helper =
    define loop(cursor: int, acc, xs) {
      if eq-int(cursor, sz) {
        Right(acc)
      } else {
        let y = _access(xs, cursor);
        try v = !consumer(acc, y, cursor);
        loop(add-int(cursor, 1), v, xs)
      }
    };
  helper(0, acc, xs)
}

inline fold-right<elem-type, result-type>(
  xs: vector(elem-type),
  acc: result-type,
  !consumer: (elem-type, result-type, int) -> result-type,
): result-type {
  let sz on xs = length(xs);
  let helper =
    define loop(cursor: int, acc: result-type, xs: vector(elem-type)): result-type {
      if eq-int(cursor, sz) {
        let xs = cast(vector(elem-type), &vector(pointer), xs);
        _free-vector(xs);
        acc
      } else {
        let element-ptr on xs = _get-element-pointer(xs, cursor);
        let old-value = cast(int, elem-type, load-int(element-ptr));
        !consumer(old-value, loop(add-int(cursor, 1), acc, xs), cursor)
      }
    };
  helper(0, acc, xs)
}

inline fold-right-N<elem-type, result-type>(
  xs: &vector(elem-type),
  acc: result-type,
  !consumer: (&elem-type, result-type, int) -> result-type,
): result-type {
  let sz = length(xs);
  let helper =
    define loop(cursor: int, acc: result-type, xs: &vector(elem-type)): result-type {
      if eq-int(cursor, sz) {
        acc
      } else {
        let old-value = _access(xs, cursor);
        !consumer(old-value, loop(add-int(cursor, 1), acc, xs), cursor)
      }
    };
  helper(0, acc, xs)
}

inline for-each<a, b>(xs: vector(a), !f: (a, int) -> b): vector(b) {
  let _ on xs = _set-elem-type(xs, b);
  let len on xs = length(xs);
  let update-values =
    define self(xs: vector(a), index: int): vector(a) {
      if ge-int(index, len) {
        xs
      } else {
        let element-ptr on xs = _get-element-pointer(xs, index);
        let old-value = cast(int, a, load-int(element-ptr));
        let new-element-ptr = cast(b, int, !f(old-value, index));
        store-int(new-element-ptr, element-ptr);
        self(xs, add-int(index, 1))
      }
    };
  cast(vector(a), vector(b), update-values(xs, 0))
}

inline for-each-N<a, b>(xs: &vector(a), f: (&a, int) -> b): vector(b) {
  let capacity = get-capacity(xs);
  fold-left-N(make(capacity), xs, function (acc, x, i) {
    push-back(acc, f(x, i))
  })
}

inline for<a>(xs: vector(a), !f: (a, int) -> unit): unit {
  let len on xs = length(xs);
  let loop =
    define self(xs: vector(a), index: int): unit {
      if ge-int(index, len) {
        let buffer-ptr on xs = _get-buffer-ptr(xs);
        free(buffer-ptr);
        free(cast(vector(a), pointer, xs))
      } else {
        let element-ptr on xs = _get-element-pointer(xs, index);
        let old-value = cast(int, a, load-int(element-ptr));
        !f(old-value, index);
        self(xs, add-int(index, 1))
      }
    };
  loop(xs, 0)
}

inline for-N<a>(xs: &vector(a), f: (&a, int) -> unit): unit {
  fold-left-N(Unit, xs, function (_, x, i) {
    f(x, i)
  })
}

define for-each-vertical<a, b>(xs: vector(a), col-size: int, !f: (a, int) -> b): vector(b) {
  assert "this.vector.for-each-vertical: col-size > 0" {
    gt-int(col-size, 0)
  };
  let _ on xs = _set-elem-type(xs, b);
  let vec-size on xs = length(xs);
  let store-values =
    define self(xs: vector(a), index: int): vector(a) {
      if ge-int(index, vec-size) {
        xs
      } else {
        let value-ptr on xs = _get-element-pointer(xs, index);
        let old-value = cast(int, a, load-int(value-ptr));
        let new-value-ptr = cast(b, int, !f(old-value, index));
        store-int(new-value-ptr, value-ptr);
        let next-index-cand = add-int(index, col-size);
        if lt-int(next-index-cand, vec-size) {
          self(xs, next-index-cand)
        } else {
          let current-column = rem-int(index, col-size);
          let next-column = add-int(current-column, 1);
          if ge-int(next-column, col-size) {
            xs
          } else {
            self(xs, next-column)
          }
        }
      }
    };
  cast(vector(a), vector(b), store-values(xs, 0))
}

inline zip-with<a, b, c>(xs: vector(a), ys: vector(b), !f: (a, b, int) -> c): vector(c) {
  let len1 on xs = length(xs);
  let len2 on ys = length(ys);
  let len = min-int(len1, len2);
  let result = make(len);
  let helper =
    define loop(acc: vector(c), cursor: int, xs: vector(a), ys: vector(b)): vector(c) {
      if eq-int(cursor, len) {
        let xs = cast(vector(a), &vector(pointer), xs);
        let ys = cast(vector(b), &vector(pointer), ys);
        _free-vector(xs);
        _free-vector(ys);
        acc
      } else {
        let x-ptr on xs = _get-element-pointer(xs, cursor);
        let y-ptr on ys = _get-element-pointer(ys, cursor);
        let x = cast(int, a, load-int(x-ptr));
        let y = cast(int, b, load-int(y-ptr));
        let z = !f(x, y, cursor);
        let acc = push-back(acc, z);
        loop(acc, add-int(cursor, 1), xs, ys)
      }
    };
  helper(result, 0, xs, ys)
}

inline zip-with-N<a, b, c>(xs: &vector(a), ys: &vector(b), f: (&a, &b, int) -> c): vector(c) {
  let len1 = length(xs);
  let len2 = length(ys);
  let len = min-int(len1, len2);
  let result = make(len);
  grow(result, len, function (acc, i) {
    let val1 = _access(xs, i);
    let val2 = _access(ys, i);
    push-back(acc, f(val1, val2, i))
  })
}

define from-list<a>(base-list: list(a)): vector(a) {
  let len on base-list = this.list.length(base-list);
  this.list.fold-left(make(len), base-list, function (acc, x) {
    push-back(acc, x)
  })
}

define to-list<a>(xs: vector(a)): list(a) {
  let len on xs = length(xs);
  let loop =
    define self(xs: vector(a), index: int): list(a) {
      if ge-int(index, len) {
        let buffer-ptr on xs = _get-buffer-ptr(xs);
        let vector-ptr = cast(vector(a), pointer, xs);
        free(buffer-ptr);
        free(vector-ptr);
        Nil
      } else {
        let value-ptr on xs = _get-element-pointer(xs, index);
        let value = cast(int, a, load-int(value-ptr));
        Cons(value, self(xs, add-int(index, 1)))
      }
    };
  loop(xs, 0)
}

define exchange<a>(xs: vector(a), index: int, value: a): either(vector(a), pair(a, vector(a))) {
  let len on xs = length(xs);
  if and(le-int(0, index), lt-int(index, len)) {
    let value-ptr on xs = _get-element-pointer(xs, index);
    let old-value = cast(int, a, load-int(value-ptr));
    store-int(cast(a, int, value), value-ptr);
    Right(Pair(old-value, xs))
  } else {
    Left(xs)
  }
}

define _swap<a>(xs: vector(a), i: int, j: int): vector(a) {
  let value-i-ptr on xs = _get-element-pointer(xs, i);
  let value-j-ptr on xs = _get-element-pointer(xs, j);
  let value-i = load-int(value-i-ptr);
  let value-j = load-int(value-j-ptr);
  store-int(value-j, value-i-ptr);
  store-int(value-i, value-j-ptr);
  xs
}

define swap<a>(xs: vector(a), i: int, j: int): vector(a) {
  let len on xs = length(xs);
  let b1 = and(le-int(0, i), lt-int(i, len));
  let b2 = and(le-int(0, j), lt-int(j, len));
  if and(b1, b2) {
    _swap(xs, i, j)
  } else {
    xs
  }
}

define reverse<a>(xs: vector(a)): vector(a) {
  let len on xs = length(xs);
  let f =
    define self(xs: vector(a), left: int, right: int): vector(a) {
      if ge-int(left, right) {
        xs
      } else {
        let xs = swap(xs, left, right);
        self(xs, add-int(left, 1), sub-int(right, 1))
      }
    };
  f(xs, 0, sub-int(len, 1))
}

define take<a>(xs: vector(a), n: int): vector(a) {
  let _ on xs = {
    let len = length(xs);
    let n = max-int(0, n);
    loop(sub-int(len, n), function (i) {
      let v = this.vector._access(xs, add-int(i, n));
      let _ = cast(&a, a, v);
      Unit
    });
    let from = _get-from(xs);
    let to = _get-to(xs);
    _set-to(xs, min-int(to, add-int(from, n)))
  };
  xs
}

define drop<a>(xs: vector(a), n: int): vector(a) {
  let _ on xs = {
    let n = min-int(max-int(0, n), length(xs));
    loop(n, function (i) {
      let v = this.vector._access(xs, i);
      let _ = cast(&a, a, v);
      Unit
    });
    _shift-from(xs, n)
  };
  xs
}

define split<a>(xs: vector(a), n: int): pair(vector(a), vector(a)) {
  let xs = cast(vector(a), &vector(a), xs);
  let len = length(xs);
  let ys = make(len);
  let n = max-int(0, n);
  let ys =
    grow(ys, sub-int(len, n), function (acc, i) {
      let v = this.vector._access(xs, add-int(i, n));
      push-back(acc, cast(&a, a, v))
    });
  let from = _get-from(xs);
  let to = _get-to(xs);
  _set-to(xs, min-int(to, add-int(from, n)));
  Pair(cast(&vector(a), vector(a), xs), ys)
}

define slice<a>(xs: vector(a), from: int, to: int): vector(a) {
  let ys = drop(xs, from);
  take(ys, sub-int(to, from))
}

define take-end<a>(xs: vector(a), n: int): vector(a) {
  let len on xs = length(xs);
  drop(xs, sub-int(len, n))
}

define drop-end<a>(xs: vector(a), n: int): vector(a) {
  let len on xs = length(xs);
  take(xs, sub-int(len, n))
}

define delete<a>(xs: vector(a), index: int): vector(a) {
  let len on xs = length(xs);
  if eq-int(0, index) {
    drop(xs, 1)
  } else-if eq-int(index, sub-int(len, 1)) {
    drop-end(xs, 1)
  } else-if and(lt-int(0, index), lt-int(index, sub-int(len, 1))) {
    let value-ptr on xs = _get-element-pointer(xs, index);
    let _ = cast(int, a, load-int(value-ptr));
    let pivot = div-int(len, 2);
    if lt-int(index, pivot) {
      let source-ptr on xs = _get-element-pointer(xs, 0);
      let target-ptr on xs = _get-element-pointer(xs, 1);
      let copy-bytes = mul-int(this.word.size, index);
      let _ = magic external llvm.memmove.p0.p0.i64(target-ptr, source-ptr, copy-bytes, 0);
      let _ on xs = _shift-from(xs, 1);
      xs
    } else {
      let next-value-index = add-int(index, 1);
      let source-ptr on xs = _get-element-pointer(xs, next-value-index);
      let target-ptr = value-ptr;
      let copy-bytes = mul-int(this.word.size, sub-int(len, next-value-index));
      let _ = magic external llvm.memmove.p0.p0.i64(target-ptr, source-ptr, copy-bytes, 0);
      let _ on xs = _shift-to(xs, -1);
      xs
    }
  } else {
    xs
  }
}

inline find-index<a>(xs: &vector(a), !pred: (&a) -> bool): int {
  let len = length(xs);
  let f =
    define self(index: int): int {
      if ge-int(index, len) {
        -1
      } else {
        let value = _access(xs, index);
        if !pred(value) {
          index
        } else {
          self(add-int(index, 1))
        }
      }
    };
  f(0)
}

inline find-index-end<a>(xs: &vector(a), !pred: (&a) -> bool): int {
  let len = length(xs);
  let f =
    define self(index: int): int {
      if lt-int(index, 0) {
        -1
      } else {
        let value = _access(xs, index);
        if !pred(value) {
          index
        } else {
          self(sub-int(index, 1))
        }
      }
    };
  f(sub-int(len, 1))
}

inline take-while<a>(xs: vector(a), !pred: (&a) -> bool): vector(a) {
  let pivot-index on xs = find-index(xs, compose(not, !pred));
  if lt-int(pivot-index, 0) {
    xs
  } else {
    take(xs, pivot-index)
  }
}

inline drop-while<a>(xs: vector(a), !pred: (&a) -> bool): vector(a) {
  let pivot-index on xs = find-index(xs, compose(not, !pred));
  if lt-int(pivot-index, 0) {
    make(1)
  } else {
    drop(xs, pivot-index)
  }
}

inline take-while-end<a>(xs: vector(a), !pred: (&a) -> bool): vector(a) {
  let pivot-index on xs = find-index-end(xs, compose(not, !pred));
  if lt-int(pivot-index, 0) {
    xs
  } else {
    drop(xs, add-int(pivot-index, 1))
  }
}

inline drop-while-end<a>(xs: vector(a), !pred: (&a) -> bool): vector(a) {
  let pivot-index on xs = find-index-end(xs, compose(not, !pred));
  if lt-int(pivot-index, 0) {
    make(0)
  } else {
    take(xs, add-int(pivot-index, 1))
  }
}

define intercalate<a>(xs: vector(a), !sep: a): vector(a) {
  let len on xs = length(xs);
  if eq-int(len, 0) {
    xs
  } else-if eq-int(len, 1) {
    xs
  } else {
    let xs = _extend-back(xs);
    let new-length = sub-int(mul-int(2, len), 1);
    let helper =
      define self(xs: vector(a), index: int): vector(a) {
        if le-int(index, 0) {
          let from on xs = _get-from(xs);
          let to = add-int(from, new-length);
          let _ on xs = _set-to(xs, to);
          xs
        } else {
          let swap-i = index;
          let swap-j = mul-int(2, index);
          let sep-index = sub-int(swap-j, 1);
          let xs = _swap(xs, swap-i, swap-j);
          let value-ptr on xs = _get-element-pointer(xs, sep-index);
          store-int(cast(a, int, !sep), value-ptr);
          self(xs, sub-int(index, 1))
        }
      };
    helper(xs, sub-int(len, 1))
  }
}

define append<a>(xs: vector(a), ys: vector(a)): vector(a) {
  this.vector.fold-left(xs, ys, function (acc, y, _) {
    push-back(acc, y)
  })
}

define concat<a>(xss: vector(vector(a))): vector(a) {
  let xss-size on xss = length(xss);
  let get-size =
    define loop(yss: &vector(vector(a)), index: int, acc: int): int {
      if eq-int(index, xss-size) {
        acc
      } else {
        let ys = _access(yss, index);
        let acc = add-int(acc, length(ys));
        loop(yss, add-int(index, 1), acc)
      }
    };
  let size on xss = get-size(xss, 0, 0);
  this.vector.fold-left(make(size), xss, function (acc, ys, _) {
    append(acc, ys)
  })
}

define product<a, b>(xs: vector(a), ys: vector(b)): vector(pair(a, b)) {
  let xyss =
    for-each(xs, function (x, _) {
      for-each(ys, function (y, _) {
        Pair(x, y)
      })
    });
  concat(xyss)
}

define zip<a, b>(xs: vector(a), ys: vector(b)): vector(pair(a, b)) {
  zip-with(xs, ys, function (x, y, _) {
    Pair(x, y)
  })
}

define unzip<a, b>(ps: vector(pair(a, b))): pair(vector(a), vector(b)) {
  let len on ps = length(ps);
  let vec-l = make(len);
  let vec-r = make(len);
  this.vector.fold-right(ps, Pair(vec-l, vec-r), function (p, acc, _) {
    let Pair(l, r) = p;
    let Pair(rest-l, rest-r) = acc;
    Pair(push-front(rest-l, l), push-front(rest-r, r))
  })
}

define partition<a, b>(xs: vector(either(a, b))): pair(vector(a), vector(b)) {
  let len on xs = length(xs);
  let vec-l = make(len);
  let vec-r = make(len);
  this.vector.fold-right(xs, Pair(vec-l, vec-r), function (elem, acc, _) {
    let Pair(ls, rs) = acc;
    match elem {
    | Left(l) =>
      Pair(push-front(ls, l), rs)
    | Right(r) =>
      Pair(ls, push-front(rs, r))
    }
  })
}

define all(xs: &vector(bool)): bool {
  this.vector.fold-left-N(True, xs, function (b1, b2, _) {
    and(b1, *b2)
  })
}

define any(xs: &vector(bool)): bool {
  this.vector.fold-left-N(True, xs, function (b1, b2, _) {
    or(b1, *b2)
  })
}

inline filter<a>(xs: vector(a), !pred: (&a) -> bool): vector(a) {
  let len on xs = length(xs);
  let helper =
    define loop(num-of-new-elems: int, cursor: int, xs: vector(a)): vector(a) {
      if eq-int(cursor, len) {
        take(xs, num-of-new-elems)
      } else {
        let pred-is-true on xs = !pred(_access(xs, cursor));
        if pred-is-true {
          let xs =
            if eq-int(num-of-new-elems, cursor) {
              xs
            } else {
              _swap(xs, num-of-new-elems, cursor)
            };
          loop(add-int(num-of-new-elems, 1), add-int(cursor, 1), xs)
        } else {
          loop(num-of-new-elems, add-int(cursor, 1), xs)
        }
      }
    };
  helper(0, 0, xs)
}

define filter-some<a>(xs: vector(?a)): vector(a) {
  let len on xs = length(xs);
  let helper =
    define loop(num-of-new-elems: int, cursor: int, xs: vector(?a)): vector(a) {
      if eq-int(cursor, len) {
        let from on xs = _get-from(xs);
        let to = add-int(from, num-of-new-elems);
        let _ on xs = _set-to(xs, to);
        let _ on xs = _set-elem-type(xs, a);
        cast(vector(?a), vector(a), xs)
      } else {
        let found-some on xs = {
          let value-ptr = _get-element-pointer(xs, cursor);
          match cast(&?a, ?a, _access(xs, cursor)) {
          | Right(v) =>
            store-int(cast(a, int, v), value-ptr);
            True
          | Left(Unit) =>
            store-int(0, value-ptr);
            False
          }
        };
        if found-some {
          let xs =
            if eq-int(num-of-new-elems, cursor) {
              xs
            } else {
              _swap(xs, num-of-new-elems, cursor)
            };
          loop(add-int(num-of-new-elems, 1), add-int(cursor, 1), xs)
        } else {
          loop(num-of-new-elems, add-int(cursor, 1), xs)
        }
      }
    };
  helper(0, 0, xs)
}

inline find<a, r := rho>(xs: &vector(a), !pred: (&a) -> bool): ?&a {
  let len = length(xs);
  let f =
    define self(r: ?unit, index: int): ?&a {
      if ge-int(index, len) {
        try _ = r;
        none
      } else {
        let value = _access(xs, index);
        if !pred(value) {
          try _ = r;
          Right(value)
        } else {
          self(r, add-int(index, 1))
        }
      }
    };
  f(r, 0)
}

inline span<a>(xs: vector(a), !pred: (&a) -> bool): pair(vector(a), vector(a)) {
  let i on xs =
    find-index(xs, function (x) {
      not(!pred(x))
    });
  if lt-int(i, 0) {
    Pair(xs, make(0))
  } else {
    split(xs, i)
  }
}

// right-open interval
define range(from: int, to: int): vector(int) {
  if ge-int(from, to) {
    make(0)
  } else {
    let size = sub-int(to, from);
    let xs = make(size);
    grow(xs, size, function (acc, i) {
      push-back(acc, add-int(i, from))
    })
  }
}

define lift-vector<a>(xs: vector(a), _: (a) -> meta a): meta vector(a) {
  magic cast(vector(a), meta vector(a), xs)
  // admissible:
  //   let base: meta vector(a) = box {make(10)};
  //   this.list.fold-left(base, to-list(xs), function (acc, x) {
  //     box {
  //       letbox v = acc;
  //       letbox x' = f(x);
  //       push-back(v, x')
  //     }
  //   })
}

define zen(): unit {
  // find
  let xs = this.vector.make(5);
  let xs = push-back(xs, 1);
  let xs = push-back(xs, 2);
  let xs = push-back(xs, 3);
  let xs = push-back(xs, 4);
  pin xs = push-back(xs, 5);
  let item =
    find(xs, function (x) {
      eq-int(*x, -1)
    });
  match item {
  | Left(_) =>
    print-line("left")
  | Right(x) =>
    print-line("right");
    print-int-line(*x);
  };
  let xs: vector(text) = make(0);
  let xs = push-back(xs, *"b");
  let xs = push-front(xs, *"a");
  let xs = push-front(xs, *"c");
  let !xs = drop(xs, 2);
  let _ = !xs;
  let _ = !xs;
  let _ = !xs;
  let xs = push-front(!xs, *"d");
  let xs = push-back(xs, *"e");
  let xs = push-front(xs, *"f");
  let xs = push-back(xs, *"g");
  let xs = push-front(xs, *"h");
  let !xs = xs;
  // let !xs = drop(xs, 3);
  let _ = !xs;
  // let !xs = drop(!xs, 3);
  let _ = !xs;
  let _ = !xs;
  let _ = push-front(!xs, *"foo");
  let _ = !xs;
  let _ = !xs;
  let _ = !xs;
  // let xs = push-back(xs, 7);
  // let xs = push-back(xs, 8);
  // let xs = push-back(xs, 9);
  // let xs = push-back(xs, 10);
  // let xs = take(xs, 5);
  // let !xs = delete(xs, 0);
  // let _ = !xs;
  // let _ = !xs;
  // let _ = !xs;
  // let _ = !xs;
  pin xs = !xs;
  let ys: vector(int) = from-list(to-list(from-list([1, 2, 3, 4, 5])));
  pin ys = ys;
  let zs =
    zip-with-N(xs, ys, function (x, y, _) {
      replicate(x, *y)
    });
  let !zs = delete(reverse(zs), 100);
  let _ = !zs;
  let _ = !zs;
  match pop-back(!zs) {
  | Left(_) =>
    print("left\n")
  | Right(Pair(!rest, z)) =>
    let _ = !rest;
    let _ = !rest;
    let len on !rest = length(!rest);
    let _ = !rest;
    print("right. last-elem = ");
    let _ on z = print-line(z);
    let _ = z;
    print("len = ");
    print-int-line(len)
  };
  pin zs = !zs;
  for(*zs, function (x, _) {
    pin x = x;
    print(x);
    print(", ")
  });
  print("\n----\n");
  for(!xs, function (x, _) {
    pin x = x;
    print(x);
    print(", ")
  });
  print("\n")
}
