import {
  this.binary {binary},
  this.binary.eq {eq-binary},
  this.bool {False, True, bool},
  this.bool.io {print-bool-line},
  this.either {Left, Right},
  this.list {Cons, List, Nil, list},
  this.magic,
  this.memory {load-int, load-pointer},
  this.pair {Pair},
  this.pointer {shift},
  this.rune.eq {eq-rune},
  this.text {text},
  this.type-tag {
    Algebraic,
    Binary,
    Enum,
    Float16,
    Float32,
    Float64,
    Int1,
    Int16,
    Int2,
    Int32,
    Int4,
    Int64,
    Int8,
    Rune,
    Vector,
    Wrapper,
  },
  this.unit {unit},
  this.vector {vector},
  this.word,
}

macro-inline _macro-length<a>(xs: list(a)): int {
  match xs {
  | Nil =>
    0
  | Cons(_, ys) =>
    add-int(1, _macro-length(ys))
  }
}

macro-inline _join-text(t1: &text, t2: &text): &text {
  let tmp = magic text-uncons(t1);
  match tmp {
  | Left(_) =>
    t2
  | Right(Pair(r, rest)) =>
    magic text-cons(r, _join-text(rest, t2))
  }
}

nominal {
  macro eq-data<a>(x: '&a, y: '&a): 'bool,
}

macro-inline _equal-vector<a>(xs: '&vector(a), ys: '&vector(a)): 'bool {
  quote {
    let len-x = this.vector.length(splice {xs});
    let len-y = this.vector.length(splice {ys});
    if eq-int(len-x, len-y) {
      this.vector.zip-fold(splice {xs}, splice {ys}, True, function (x: &a, y: &a, acc: bool, _: int): bool {
        if acc {
          splice {eq-data(quote {x}, quote {y})}
        } else {
          False
        }
      })
    } else {
      False
    }
  }
}

inline _load-nth-element(data-arg-num: int, cursor: int, x: pointer): pointer {
  let u = this.word.size;
  let offset = mul-int(u, add-int(1, add-int(data-arg-num, cursor))); // x = (disc, data-args, cons-args)
  let elem-ptr = shift(x, offset);
  load-pointer(elem-ptr)
}

macro-inline _eq-data-adt-cons<a>(
  cons-arg-types: list(type),
  cursor: int,
  x: '&a,
  y: '&a,
): 'bool {
  match cons-arg-types {
  | Nil =>
    quote {True}
  | Cons(t, rest) =>
    letveil t = t;
    let data-arg-num = _macro-length(magic get-data-args(a));
    quote {
      let data-arg-num = splice {promote {data-arg-num}};
      let low-cursor = splice {promote {cursor}};
      let x-ptr = this.magic.cast<&a, pointer>(splice {x});
      let y-ptr = this.magic.cast<&a, pointer>(splice {y});
      let x-elem-ptr = this.magic.cast<pointer, &t>(_load-nth-element(data-arg-num, low-cursor, x-ptr));
      let y-elem-ptr = this.magic.cast<pointer, &t>(_load-nth-element(data-arg-num, low-cursor, y-ptr));
      let is-eq = splice {eq-data(quote {x-elem-ptr}, quote {y-elem-ptr})};
      if is-eq {
        splice {_eq-data-adt-cons(rest, add-int(cursor, 1), x, y)}
      } else {
        False
      }
    }
  }
}

macro-inline _eq-data-adt<a>(cons-index: int, cons-num: int, disc: 'int, x: '&a, y: '&a): 'bool {
  if eq-int(cons-index, cons-num) {
    quote {True}
  } else {
    let ts = magic get-constructor-arg-types(a, cons-index);
    quote {
      if eq-int(splice {promote {cons-index}}, splice {disc}) {
        splice {_eq-data-adt-cons(ts, 0, x, y)}
      } else {
        splice {_eq-data-adt(add-int(cons-index, 1), cons-num, disc, x, y)}
      }
    }
  }
}

macro eq-data<a>(x: '&a, y: '&a): 'bool {
  let k = magic get-type-tag(a);
  match k {
  | Algebraic =>
    let num = magic get-cons-size(a);
    quote {
      let ptr-x = this.magic.cast<&a, pointer>(splice {x});
      let ptr-y = this.magic.cast<&a, pointer>(splice {y});
      let discriminant-x = load-int(ptr-x);
      let discriminant-y = load-int(ptr-y);
      if ne-int(discriminant-x, discriminant-y) {
        False
      } else {
        splice {_eq-data-adt(0, num, quote {discriminant-x}, x, y)}
      }
    }
  | Enum =>
    quote {
      let discriminant-x = this.magic.cast<&a, int>(splice {x});
      let discriminant-y = this.magic.cast<&a, int>(splice {y});
      eq-int(discriminant-x, discriminant-y)
    }
  | Int1 =>
    quote {
      let x = this.magic.cast<&a, int1>(splice {x});
      let y = this.magic.cast<&a, int1>(splice {y});
      eq-int1(x, y)
    }
  | Int2 =>
    quote {
      let x = this.magic.cast<&a, int2>(splice {x});
      let y = this.magic.cast<&a, int2>(splice {y});
      eq-int2(x, y)
    }
  | Int4 =>
    quote {
      let x = this.magic.cast<&a, int4>(splice {x});
      let y = this.magic.cast<&a, int4>(splice {y});
      eq-int4(x, y)
    }
  | Int8 =>
    quote {
      let x = this.magic.cast<&a, int8>(splice {x});
      let y = this.magic.cast<&a, int8>(splice {y});
      eq-int8(x, y)
    }
  | Int16 =>
    quote {
      let x = this.magic.cast<&a, int16>(splice {x});
      let y = this.magic.cast<&a, int16>(splice {y});
      eq-int16(x, y)
    }
  | Int32 =>
    quote {
      let x = this.magic.cast<&a, int32>(splice {x});
      let y = this.magic.cast<&a, int32>(splice {y});
      eq-int32(x, y)
    }
  | Int64 =>
    quote {
      let x = this.magic.cast<&a, int64>(splice {x});
      let y = this.magic.cast<&a, int64>(splice {y});
      eq-int64(x, y)
    }
  | Float16 =>
    quote {
      let x = this.magic.cast<&a, float16>(splice {x});
      let y = this.magic.cast<&a, float16>(splice {y});
      eq-float16(x, y)
    }
  | Float32 =>
    quote {
      let x = this.magic.cast<&a, float32>(splice {x});
      let y = this.magic.cast<&a, float32>(splice {y});
      eq-float32(x, y)
    }
  | Float64 =>
    quote {
      let x = this.magic.cast<&a, float64>(splice {x});
      let y = this.magic.cast<&a, float64>(splice {y});
      eq-float64(x, y)
    }
  | Rune =>
    quote {
      let x = this.magic.cast<&a, rune>(splice {x});
      let y = this.magic.cast<&a, rune>(splice {y});
      eq-rune(x, y)
    }
  | Binary =>
    quote {
      let x = this.magic.cast<&a, &binary>(splice {x});
      let y = this.magic.cast<&a, &binary>(splice {y});
      eq-binary(x, y)
    }
  | Vector =>
    letveil t = magic get-vector-content-type(a);
    quote {
      let x: &vector(t) = this.magic.cast<&a, &vector(t)>(splice {x});
      let y: &vector(t) = this.magic.cast<&a, &vector(t)>(splice {y});
      splice {_equal-vector<t>(quote {x}, quote {y})}
    }
  | Wrapper =>
    letveil t = magic get-wrapper-content-type(a);
    quote {
      let x: &t = this.magic.cast<&a, &t>(splice {x});
      let y: &t = this.magic.cast<&a, &t>(splice {y});
      splice {eq-data(quote {x}, quote {y})}
    }
  | _ =>
    let t = magic show-type(a);
    let message = _join-text("eq-data doesn't support: ", t);
    magic compile-error(message)
  }
}

define zen(): unit {
  pin xs: vector(list(int)) = this.vector.Vector[List[], List[10, 20]];
  pin ys: vector(list(int)) = this.vector.Vector[List[], List[10, 20]];
  let foo = splice {eq-data(quote {xs}, quote {ys})};
  // pin f: list((int) -> int) =
  //   List[function (x) {
  //     x
  //   }];
  // pin g: list((int) -> int) =
  //   List[function (x) {
  //     x
  //   }];
  // let foo = splice {eq-data(quote {f}, quote {g})};
  print-bool-line(foo);
}
