import {
  Ext,
  FD,
  Word,
  this.bool {bool},
  this.c-int {c-int, eq-c-int, from-c-int},
  this.c-pid {c-pid, eq-c-pid},
  this.c-size {C-Size},
  this.either {Left, Right},
  this.errno {Errno, report-system-error, system},
  this.external {free},
  this.file {close},
  this.file.descriptor {stderr, stdin, stdout},
  this.foreign {_wcoredump, _wexitstatus, _wifexited, _wifsignaled, _wifstopped, _wstopsig, _wtermsig},
  this.list {Cons, fold-left-with-index, length, list},
  this.memory {load-c-int, store-int},
  this.pair {Pair, pair},
  this.pointer {extract, null-pointer, shift},
  this.text {_get-content-pointer, text},
  this.text.io {print, print-int, read, write},
  this.unit {Unit, unit},
}

foreign {
  execvp(pointer, pointer): c-int,
  fork(): c-pid,
  waitpid(c-pid, pointer, c-int): c-pid,
  pipe(pointer): c-int,
  dup2(c-int, c-int): c-int,
}

define __waitpid(pid: c-pid, status-ptr: pointer, option: c-int): c-pid {
  magic external waitpid(pid, status-ptr, option)
}

data process-result-status {
| Exited(status: c-int)
| Signaled(termsig: c-int, coredump: bool)
| Stopped(stopsig: c-int)
}

define _waitpid(pid: c-pid): system(process-result-status) {
  let status-ptr = Ext.malloc(C-Size(Word.size)) in
  let result = __waitpid(pid, status-ptr, 0) in
  if eq-c-pid(result, -1) {
    free(status-ptr);
    report-system-error()
  } else {
    let stat-loc = load-c-int(status-ptr) in
    free(status-ptr);
    if _wifexited(stat-loc) {
      let exit-status = _wexitstatus(stat-loc) in
      Right(Exited(exit-status))
    } else-if _wifsignaled(stat-loc) {
      let termsig = _wtermsig(stat-loc) in
      let coredump = _wcoredump(stat-loc) in
      Right(Signaled(termsig, coredump))
    } else-if _wifstopped(stat-loc) {
      let stopsig = _wstopsig(stat-loc) in
      Right(Stopped(stopsig))
    } else {
      // unreachable
      report-system-error()
    }
  }
}

define _make-argv(xs: list(pointer)): pointer {
  let len on xs = length(xs) in
  let ptr = Ext.malloc(C-Size(mul-int(Word.size, len))) in
  fold-left-with-index(Unit, xs, function (_, x, i) {
    store-int(extract(x), shift(ptr, mul-int(Word.size, i)))
  });
  ptr
}

define _create-pipe(): system(pair(FD.descriptor, FD.descriptor)) {
  let ptr = Ext.malloc(C-Size(mul-int(Word.size, 2))) in
  let result = magic external pipe(ptr) in
  if eq-c-int(result, -1) {
    free(ptr);
    report-system-error()
  } else {
    let reader = magic load(int32, ptr) in
    let writer = magic load(int32, shift(ptr, 4)) in
    free(ptr);
    Right(Pair(reader, writer))
  }
}

define _dup2(fd1: FD.descriptor, fd2: FD.descriptor): system(unit) {
  let fd1 = magic cast(FD.descriptor, c-int, fd1) in
  let fd2 = magic cast(FD.descriptor, c-int, fd2) in
  let result = magic external dup2(fd1, fd2) in
  if eq-c-int(result, -1) {
    report-system-error()
  } else {
    Right(Unit)
  }
}

define _fork(): system(c-pid) {
  let result = magic external fork() in
  if eq-c-pid(result, -1) {
    report-system-error()
  } else {
    Right(result)
  }
}

data process-spec {
| Process-Spec(
    process: &text,
    args: list(&text),
    input: &text,
  )
}

data process-result {
| Process-Result(
    status: process-result-status,
    stdout-text: text,
    stderr-text: text,
  )
}

define spawn(
  ps: process-spec,
  stdout-initial-buffer-size: int,
  stderr-initial-buffer-size: int,
): system(process-result) {
  try Pair(in-reader, in-writer) = _create-pipe() in
  try Pair(out-reader, out-writer) = _create-pipe() in
  try Pair(error-reader, error-writer) = _create-pipe() in
  try pid = _fork() in
  if eq-c-pid(pid, -1) {
    report-system-error()
  } else-if eq-c-pid(pid, 0) {
    try _ = close(in-writer) in
    try _ = close(out-reader) in
    try _ = close(error-reader) in
    try _ = _dup2(in-reader, stdin) in
    try _ = _dup2(out-writer, stdout) in
    try _ = _dup2(error-writer, stderr) in
    use ps {process, args} in
    let raw-args = this.list.for-each(Cons(process, args), _get-content-pointer) in
    let cmd-args = _make-argv(this.list.append(raw-args, [null-pointer])) in
    let cmd = _get-content-pointer(process) in
    let _ = magic external execvp(cmd, cmd-args) in
    report-system-error()
  } else {
    try _ = close(in-reader) in
    try _ = close(out-writer) in
    try _ = close(error-writer) in
    use ps {input} in
    try _ = write(input, in-writer) in
    try _ = close(in-writer) in
    try stdout-text = read(out-reader, stdout-initial-buffer-size) in
    try stderr-text = read(error-reader, stderr-initial-buffer-size) in
    try status = _waitpid(pid) in
    Right(Process-Result of {
      status,
      stdout-text,
      stderr-text,
    })
  }
}

define _print-process-result-status(st: process-result-status): unit {
  match st {
  | Exited(status) =>
    print-int(from-c-int(status))
  | Signaled(_, _) =>
    Unit
  | Stopped(_) =>
    Unit
  }
}

define zen(): unit {
  let cmd =
    Process-Spec of {
      process = "sh",
      args = ["-c", "\"ls /\""],
      input = "",
    }
  in
  match spawn(cmd, 1024, 1024) {
  | Right(result) =>
    use result {stdout-text, stderr-text, status} in
    let _ on stdout-text = print(stdout-text) in
    let _ on stderr-text = print(stderr-text) in
    let _ = stdout-text in
    let _ = stderr-text in
    print("exit-code: ");
    _print-process-result-status(status);
    print("\n");
    print("success\n")
  | Left(Errno(num)) =>
    print("fail: ");
    print-int(from-c-int(num))
  }
}
