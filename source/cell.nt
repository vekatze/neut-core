import {
  Ext,
  List,
  Magic,
  Mem,
  Word,
  this.box {axiom-T},
  this.c-size {C-Size, from-c-size},
  this.foreign {THREAD_COND_SIZE, THREAD_MUTEX_SIZE},
  this.int {sum-int},
  this.pair {Pair, pair},
  this.pointer {shift},
  this.unit {Unit, unit},
}

inline _get-clone(a: type, x: &a): a {
  *x
}

nominal {
  _cell: type,
}

data cell(_: type) {
| _Cell(_cell)
}

define _new(a: type, val: a): cell(a) {
  let mutex-size = from-c-size(THREAD_MUTEX_SIZE) in
  let cond-size = from-c-size(THREAD_COND_SIZE) in
  let u = Word.size in
  let ptr = Ext.malloc(C-Size(sum-int([u, mutex-size, cond-size, u]))) in
  Mem.store-int(Magic.cast(a, int, val), ptr);
  let _ = Ext.thread-mutex-init(shift(ptr, u)) in
  Ext.thread-cond-init(shift(ptr, add-int(u, mutex-size)));
  Mem.store-int(Magic.cast(type, int, a), shift(ptr, add-int(u, add-int(mutex-size, cond-size))));
  Magic.cast(pointer, cell(a), ptr)
}

// (content, thread-mutex, thread-cond)
resource _cell {
  function (ch: pointer) {
    let u = Word.size in
    let mutex-size = from-c-size(THREAD_MUTEX_SIZE) in
    let cond-size = from-c-size(THREAD_COND_SIZE) in
    let cond-offset = add-int(u, mutex-size) in
    let type-offset = add-int(u, add-int(mutex-size, cond-size)) in
    let a = Magic.cast(int, type, Mem.load-int(shift(ch, type-offset))) in
    Ext.thread-cond-destroy(shift(ch, cond-offset));
    Ext.thread-mutex-destroy(shift(ch, u));
    let _: a = magic cast(int, a, Mem.load-int(ch)) in
    Ext.free(ch)
  },
  function (ch: pointer) {
    let u = Word.size in
    let mutex-size = from-c-size(THREAD_MUTEX_SIZE) in
    let cond-size = from-c-size(THREAD_COND_SIZE) in
    let type-offset = add-int(u, add-int(mutex-size, cond-size)) in
    let a = Magic.cast(int, type, Mem.load-int(shift(ch, type-offset))) in
    let val: &a = Magic.cast(int, &a, Mem.load-int(ch)) in
    let val-clone: a = _get-clone(a, val) in
    Magic.cast(cell(a), pointer, _new(a, val-clone))
  },
}

inline _new-cell<a>(initial-value: a): cell(a) {
  _new(a, initial-value)
}

inline modify<a, b>(ch: &cell(a), f: (a) -> pair(a, b)): b {
  let ch = Magic.cast(&cell(a), pointer, ch) in
  let u = Word.size in
  let mutex-ptr = shift(ch, u) in
  Ext.thread-mutex-lock(mutex-ptr);
  let val-base: int = Mem.load-int(ch) in
  let orig-val = Magic.cast(int, a, val-base) in
  let Pair(new-val, result) = f(orig-val) in
  Mem.store-int(Magic.cast(a, int, new-val), ch);
  let mutex-size = from-c-size(THREAD_MUTEX_SIZE) in
  Ext.thread-cond-signal(shift(ch, add-int(u, mutex-size)));
  Ext.thread-mutex-unlock(mutex-ptr);
  result
}

inline mutate<a>(ch: &cell(a), f: (a) -> a): unit {
  modify(ch, function (x) {
    Pair(f(x), Unit)
  })
}

inline borrow<a, b>(ch: &cell(a), f: (&a) -> meta b): b {
  let ch = Magic.cast(&cell(a), pointer, ch) in
  let u = Word.size in
  let mutex-ptr = shift(ch, u) in
  Ext.thread-mutex-lock(mutex-ptr);
  let val-base: int = Mem.load-int(ch) in
  let val: &a = Magic.cast(int, &a, val-base) in
  let result = f(val) in
  let mutex-size = from-c-size(THREAD_MUTEX_SIZE) in
  Ext.thread-cond-signal(shift(ch, add-int(u, mutex-size)));
  Ext.thread-mutex-unlock(mutex-ptr);
  axiom-T(result)
}

define clone<a>(ch: &cell(a)): a {
  borrow(ch, function (val: &a) {
    box val {val}
  })
}

define extract<a>(c: cell(a)): a {
  let u = Word.size in
  let ch = Magic.cast(cell(a), pointer, c) in
  let result = Magic.cast(int, a, Mem.load-int(ch)) in
  Ext.thread-mutex-destroy(shift(ch, u));
  Ext.thread-cond-destroy(shift(ch, add-int(u, from-c-size(THREAD_MUTEX_SIZE))));
  Ext.free(ch);
  result
}
