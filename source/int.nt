import {
  this.bool,
  this.list {Cons, Nil, list},
}

define sum-int(xs: list(int)): int {
  let f =
    define loop(ys: list(int), acc: int): int {
      match ys {
      | Nil =>
        acc
      | Cons(z, zs) =>
        loop(zs, add-int(z, acc))
      }
    }
  in
  f(xs, 0)
}

define min-int(x: int, y: int): int {
  if lt-int(x, y) {
    x
  } else {
    y
  }
}

define max-int(x: int, y: int): int {
  if lt-int(x, y) {
    y
  } else {
    x
  }
}

define abs-int(x: int): int {
  if lt-int(x, 0) {
    mul-int(-1, x)
  } else {
    x
  }
}

define power-int(a: int, b: int): int {
  let f =
    define _power(a: int, b: int, result: int): int {
      if le-int(b, 0) {
        result
      } else-if eq-int(rem-int(b, 2), 0) {
        _power(mul-int(a, a), div-int(b, 2), result)
      } else {
        _power(mul-int(a, a), div-int(b, 2), mul-int(result, a))
      }
    }
  in
  f(a, b, 1)
}

inline int-max: int {
  9_223_372_036_854_775_807
}

inline int-min: int {
  -9_223_372_036_854_775_808
}
