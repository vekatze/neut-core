import {
  this.bool,
  this.box,
  this.either {Right, none, region},
  this.list {Cons, Nil, append, list},
  this.ord {Ord, ord},
  this.ordering {EQ, GT, LT},
  this.pair {Pair, pair},
  this.text,
  this.trick {panic},
  this.unit {Unit},
}

// size balanced tree
data dictionary(k, v) {
| Tip
| Bin(
    size: int,
    key: k,
    value: v,
    left: dictionary(k, v),
    right: dictionary(k, v),
  )
}

inline size<k, v>(d: &dictionary(k, v)): int {
  case d {
  | Tip =>
    0
  | Bin of {size} =>
    *size
  }
}

inline empty<k, v>: dictionary(k, v) {
  Tip
}

inline singleton<k, v>(key: k, value: v): dictionary(k, v) {
  Bin of {size := 1, left := Tip, right := Tip, key, value}
}

inline _delta: int {
  3
}

inline _ratio: int {
  2
}

inline _balance-L<a, b>(k: a, x: b, l: dictionary(a, b), r: dictionary(a, b)): dictionary(a, b) {
  match r {
  | Tip =>
    match l {
    | Tip =>
      singleton(k, x)
    | Bin(ls, lk, lx, ll, lr) =>
      match ll, lr {
      | Tip, Tip =>
        Bin(2, k, x, Bin(ls, lk, lx, Tip, Tip), Tip)
      | Tip, Bin(_, lrk, lrx, _, _) =>
        Bin(3, lrk, lrx, Bin(1, lk, lx, Tip, Tip), Bin(1, k, x, Tip, Tip))
      | Bin(lls, llk, llx, lll, llr), Tip =>
        Bin(3, lk, lx, Bin(lls, llk, llx, lll, llr), Bin(1, k, x, Tip, Tip))
      | Bin(lls, llk, llx, lll, llr), Bin(lrs, lrk, lrx, lrl, lrr) =>
        if lt-int(lrs, mul-int(_ratio, lls)) {
          let ll = Bin(lls, llk, llx, lll, llr);
          let lr = Bin(lrs, lrk, lrx, lrl, lrr);
          Bin(add-int(1, ls), lk, lx, ll, Bin(add-int(1, lrs), k, x, lr, Tip))
        } else {
          let ll = Bin(lls, llk, llx, lll, llr);
          let lrls on lrl = size(lrl);
          let lrrs on lrr = size(lrr);
          let ls' = add-int(1, add-int(lls, lrls));
          let rs' = add-int(1, lrrs);
          Bin(add-int(1, ls), lrk, lrx, Bin(ls', lk, lx, ll, lrl), Bin(rs', k, x, lrr, Tip))
        }
      }
    }
  | Bin(rs, rk, rx, rl, rr) =>
    match l {
    | Tip =>
      let r = Bin(rs, rk, rx, rl, rr);
      Bin(add-int(1, rs), k, x, Tip, r)
    | Bin(ls, lk, lx, ll, lr) =>
      if gt-int(ls, mul-int(_delta, rs)) {
        match ll, lr {
        | Bin(lls, llk, llx, lll, llr), Bin(lrs, lrk, lrx, lrl, lrr) =>
          let lrls on lrl = size(lrl);
          let lrrs on lrr = size(lrr);
          let ll = Bin(lls, llk, llx, lll, llr);
          let r = Bin(rs, rk, rx, rl, rr);
          if lt-int(lrs, mul-int(_ratio, lls)) {
            let s' = add-int(1, add-int(ls, rs));
            let rs' = add-int(1, add-int(rs, lrs));
            Bin(s', lk, lx, ll, Bin(rs', k, x, Bin(lrs, lrk, lrx, lrl, lrr), r))
          } else {
            let s' = add-int(1, add-int(ls, rs));
            let ls' = add-int(1, add-int(lls, lrls));
            let rs' = add-int(1, add-int(rs, lrrs));
            Bin(s', lrk, lrx, Bin(ls', lk, lx, ll, lrl), Bin(rs', k, x, lrr, r))
          }
        | _, _ =>
          // unreachable
          let !lk = lk;
          let !lx = lx;
          let !tip = panic("failure in (core).dictionary._balance-L");
          Bin(ls, !lk, !lx, Bin(ls, !lk, !lx, !tip, !tip), Bin(ls, !lk, !lx, !tip, !tip))
        }
      } else {
        let s' = add-int(1, add-int(ls, rs));
        let l = Bin(ls, lk, lx, ll, lr);
        let r = Bin(rs, rk, rx, rl, rr);
        Bin(s', k, x, l, r)
      }
    }
  }
}

inline _balance-R<a, b>(k: a, x: b, l: dictionary(a, b), r: dictionary(a, b)): dictionary(a, b) {
  match l {
  | Tip =>
    match r {
    | Tip =>
      singleton(k, x)
    | Bin(rs, rk, rx, rl, rr) =>
      match rl, rr {
      | Tip, Tip =>
        Bin(2, k, x, Tip, Bin(rs, rk, rx, Tip, Tip))
      | Tip, Bin(rrs, rrk, rrx, rrl, rrr) =>
        Bin(3, rk, rx, Bin(1, k, x, Tip, Tip), Bin(rrs, rrk, rrx, rrl, rrr))
      | Bin(_, rlk, rlx, _, _), Tip =>
        Bin(3, rlk, rlx, Bin(1, k, x, Tip, Tip), Bin(1, rk, rx, Tip, Tip))
      | Bin(rls, rlk, rlx, rll, rlr), Bin(rrs, rrk, rrx, rrl, rrr) =>
        if lt-int(rls, mul-int(_ratio, rrs)) {
          let rl = Bin(rls, rlk, rlx, rll, rlr);
          let rr = Bin(rrs, rrk, rrx, rrl, rrr);
          Bin(add-int(1, rs), rk, rx, Bin(add-int(1, rls), k, x, Tip, rl), rr)
        } else {
          let rr = Bin(rrs, rrk, rrx, rrl, rrr);
          let rlrs on rlr = size(rlr);
          let rlls on rll = size(rll);
          let rs' = add-int(1, rlls);
          let ls' = add-int(1, add-int(rrs, rlrs));
          Bin(add-int(1, rs), rlk, rlx, Bin(rs', k, x, Tip, rll), Bin(ls', rk, rx, rlr, rr))
        }
      }
    }
  | Bin(ls, lk, lx, ll, lr) =>
    match r {
    | Tip =>
      let l = Bin(ls, lk, lx, ll, lr);
      Bin(add-int(1, ls), k, x, l, Tip)
    | Bin(rs, rk, rx, rl, rr) =>
      if gt-int(rs, mul-int(_delta, ls)) {
        match rl, rr {
        | Bin(rls, rlk, rlx, rll, rlr), Bin(rrs, rrk, rrx, rrl, rrr) =>
          let rlls on rll = size(rll);
          let rlrs on rlr = size(rlr);
          let rr = Bin(rrs, rrk, rrx, rrl, rrr);
          let l = Bin(ls, lk, lx, ll, lr);
          if lt-int(rls, mul-int(_ratio, rrs)) {
            let s' = add-int(1, add-int(ls, rs));
            let rs' = add-int(1, add-int(rs, rls));
            Bin(s', rk, rx, Bin(rs', k, x, l, Bin(rls, rlk, rlx, rll, rlr)), rr)
          } else {
            let s' = add-int(1, add-int(ls, rs));
            let ls' = add-int(1, add-int(ls, rlls));
            let rs' = add-int(1, add-int(rrs, rlrs));
            Bin(s', rlk, rlx, Bin(ls', k, x, l, rll), Bin(rs', rk, rx, rlr, rr))
          }
        | _, _ =>
          // unreachable
          let !rk = rk;
          let !rx = rx;
          let !tip = panic("failure in (core).dictionary._balance-L");
          Bin(rs, !rk, !rx, Bin(rs, !rk, !rx, !tip, !tip), Bin(rs, !rk, !rx, !tip, !tip))
        }
      } else {
        let s' = add-int(1, add-int(ls, rs));
        let l = Bin(ls, lk, lx, ll, lr);
        let r = Bin(rs, rk, rx, rl, rr);
        Bin(s', k, x, l, r)
      }
    }
  }
}

define insert<k, v>(L: &ord(k), d: dictionary(k, v), key-X: k, val-X: v): dictionary(k, v) {
  tie Ord of {compare := !compare} = L;
  match d {
  | Tip =>
    singleton(key-X, val-X)
  | Bin(size, key-Y, val-Y, left, right) =>
    let order on key-X, key-Y = !compare(key-X, key-Y);
    match order {
    | LT =>
      _balance-L(key-Y, val-Y, insert(L, left, key-X, val-X), right)
    | GT =>
      _balance-R(key-Y, val-Y, left, insert(L, right, key-X, val-X))
    | EQ =>
      Bin(size, key-X, val-X, left, right)
    }
  }
}

define _lookup<k, v>(L: &ord(k), d: &dictionary(k, v), needle: &k, reg: region): ?&v {
  tie Ord of {compare} = L;
  case d {
  | Tip =>
    try _ = reg;
    none
  | Bin(_, k, x, l, r) =>
    match compare(needle, k) {
    | LT =>
      _lookup(L, l, needle, reg)
    | GT =>
      _lookup(L, r, needle, reg)
    | EQ =>
      try _ = reg;
      Right(x)
    }
  }
}

inline lookup<k, v>(L: &ord(k), d: &dictionary(k, v), needle: &k): ?&v {
  _lookup(L, d, needle, Right(Unit))
}

data _view(k, a) {
| _View(
    key: k,
    value: a,
    dictionary: dictionary(k, a),
  )
}

define _min-view-sure<a, b>(k: a, x: b, l: dictionary(a, b), r: dictionary(a, b)): _view(a, b) {
  match l {
  | Tip =>
    _View(k, x, r)
  | Bin(_, lk, lx, ll, lr) =>
    let _View of {key, value, dictionary := l'} = _min-view-sure(lk, lx, ll, lr);
    _View(key, value, _balance-R(k, x, l', r))
  }
}

define _max-view-sure<a, b>(k: a, x: b, l: dictionary(a, b), r: dictionary(a, b)): _view(a, b) {
  match r {
  | Tip =>
    _View(k, x, l)
  | Bin(_, rk, rx, rl, rr) =>
    let _View of {key, value, dictionary := r'} = _max-view-sure(rk, rx, rl, rr);
    _View(key, value, _balance-L(k, x, l, r'))
  }
}

inline _glue<k, v>(d1: dictionary(k, v), d2: dictionary(k, v)): dictionary(k, v) {
  match d1, d2 {
  | Tip, d2 =>
    d2
  | d1, Tip =>
    d1
  | Bin(ls, lk, lx, ll, lr), Bin(rs, rk, rx, rl, rr) =>
    if gt-int(ls, rs) {
      let _View of {key, value, dictionary := l'} = _max-view-sure(lk, lx, ll, lr);
      _balance-R(key, value, l', Bin(rs, rk, rx, rl, rr))
    } else {
      let _View of {key, value, dictionary := r'} = _min-view-sure(rk, rx, rl, rr);
      _balance-L(key, value, Bin(ls, lk, lx, ll, lr), r')
    }
  }
}

define delete<k, v>(L: &ord(k), d: dictionary(k, v), needle: k): dictionary(k, v) {
  tie Ord of {compare} = L;
  match d {
  | Tip =>
    Tip
  | Bin(_, kx, x, l, r) =>
    let order on needle, kx = compare(needle, kx);
    match order {
    | LT =>
      _balance-R(kx, x, delete(L, l, needle), r)
    | GT =>
      _balance-L(kx, x, l, delete(L, r, needle))
    | EQ =>
      _glue(l, r)
    }
  }
}

inline from-list<k, v>(_ord: &ord(k), kvs: list(pair(k, v))): dictionary(k, v) {
  let f =
    define self(kvs: list(pair(k, v)), acc: dictionary(k, v)): dictionary(k, v) {
      match kvs {
      | Nil =>
        acc
      | Cons(Pair(k, v), rest) =>
        self(rest, insert(_ord, acc, k, v))
      }
    };
  f(kvs, Tip)
}

define to-list<k, v>(d: dictionary(k, v)): list(pair(k, v)) {
  match d {
  | Tip =>
    []
  | Bin(_, k, v, d1, d2) =>
    let xs = to-list(d1);
    let ys = to-list(d2);
    append(xs, Cons(Pair(k, v), ys))
  }
}
