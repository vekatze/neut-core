import {
  Ext,
  L,
  Magic,
  Mem,
  Word,
  this.bool {bool},
  this.box,
  this.c-size {C-Size},
  this.control {discard},
  this.either {Right, none},
  this.external {free, malloc},
  this.int {max-int, min-int},
  this.list {Cons, Nil, fold-left-N, length, list},
  this.pointer {shift},
  this.unit {Unit, unit},
}

foreign {
  llvm.memcpy.p0.p0.i64(pointer, pointer, int64, int1): void,
}

define _copy-binary(source-pointer: pointer, target-pointer: pointer, len: int): unit {
  let _ = magic external llvm.memcpy.p0.p0.i64(target-pointer, source-pointer, len, 0) in
  Unit
}

// int8-array
// internal representation: (head-index, last-index, byte-0, byte-1, ..., byte-n)
resource binary {
  function (x: pointer) {
    free(x)
  },
  function (source-pointer: pointer) {
    let u = Word.size in
    let u2 = mul-int(u, 2) in
    let from = Mem.load-int(source-pointer) in
    let to = Mem.load-int(shift(source-pointer, u)) in
    let length = sub-int(to, from) in
    let target-pointer = malloc(C-Size(add-int(length, u2))) in
    Mem.store-int(0, target-pointer);
    Mem.store-int(length, shift(target-pointer, u));
    let source-pointer' = shift(source-pointer, add-int(u2, from)) in
    _copy-binary(source-pointer', shift(target-pointer, u2), length);
    target-pointer
  },
}

define binary-length(xs: &binary): int {
  let source-pointer = magic cast(&binary, pointer, xs) in
  let u = Word.size in
  let from = Mem.load-int(source-pointer) in
  let to = Mem.load-int(shift(source-pointer, u)) in
  sub-int(to, from)
}

define allocate(len: int): binary {
  let u = Word.size in
  let u2 = mul-int(u, 2) in
  let base-pointer = malloc(C-Size(add-int(len, u2))) in
  Mem.store-int(0, base-pointer);
  Mem.store-int(len, shift(base-pointer, u));
  let content-pointer = shift(base-pointer, u2) in
  let store-values =
    define self(index: int): unit {
      if ge-int(index, len) {
        Unit
      } else {
        Mem.store-int8(0, shift(content-pointer, index));
        self(add-int(index, 1))
      }
    }
  in
  let _ = store-values(0) in
  Magic.cast(pointer, binary, base-pointer)
}

define binary-new(xs: list(int8)): binary {
  let len on xs = length(xs) in
  let u = Word.size in
  let u2 = mul-int(u, 2) in
  let base-pointer = malloc(C-Size(add-int(len, u2))) in
  Mem.store-int(0, base-pointer);
  Mem.store-int(len, shift(base-pointer, u));
  let content-pointer = shift(base-pointer, u2) in
  let store-values =
    define self(values: list(int8), index: int): unit {
      match values {
      | Nil =>
        Unit
      | Cons(v, rest) =>
        Mem.store-int8(v, shift(content-pointer, index));
        self(rest, add-int(index, 1))
      }
    }
  in
  let _ = store-values(xs, 0) in
  Magic.cast(pointer, binary, base-pointer)
}

define binary-access(xs: &binary, i: int): int8 {
  let ptr = Magic.cast(&binary, pointer, xs) in
  let u = Word.size in
  let u2 = mul-int(u, 2) in
  Mem.load-int8(shift(ptr, add-int(u2, i)))
}

define _get-content-pointer(t: &binary): pointer {
  let ptr = Magic.cast(&binary, pointer, t) in
  let from = Mem.load-int(ptr) in
  let u = Word.size in
  shift(ptr, add-int(mul-int(u, 2), from))
}

define join(xs: list(binary)): binary {
  let len on xs =
    fold-left-N(0, xs, function (acc, x) {
      add-int(acc, binary-length(x))
    })
  in
  let u = Word.size in
  let u2 = mul-int(u, 2) in
  let new-binary = Ext.malloc(C-Size(add-int(len, u2))) in
  Mem.store-int(0, new-binary);
  Mem.store-int(len, shift(new-binary, u));
  let content-ptr = shift(new-binary, u2) in
  let loop =
    define self(offset: int, xs: list(binary)): unit {
      match xs {
      | Nil =>
        Unit
      | Cons(x, rest) =>
        let current-content-ptr = shift(content-ptr, offset) in
        let x-ptr on x = _get-content-pointer(x) in
        let x-len on x = binary-length(x) in
        _copy-binary(x-ptr, current-content-ptr, x-len);
        discard(x);
        self(add-int(offset, x-len), rest)
      }
    }
  in
  loop(0, xs);
  magic cast(pointer, binary, new-binary)
}

define join-N(xs: list(&binary)): binary {
  let len on xs =
    fold-left-N(0, xs, function (acc, x) {
      add-int(acc, binary-length(*x))
    })
  in
  let u = Word.size in
  let u2 = mul-int(u, 2) in
  let new-binary = Ext.malloc(C-Size(add-int(len, u2))) in
  Mem.store-int(0, new-binary);
  Mem.store-int(len, shift(new-binary, u));
  let content-ptr = shift(new-binary, u2) in
  let loop =
    define self(offset: int, xs: list(&binary)): unit {
      match xs {
      | Nil =>
        Unit
      | Cons(x, rest) =>
        let current-content-ptr = shift(content-ptr, offset) in
        let x-ptr = _get-content-pointer(x) in
        let x-len = binary-length(x) in
        _copy-binary(x-ptr, current-content-ptr, x-len);
        discard(x);
        self(add-int(offset, x-len), rest)
      }
    }
  in
  loop(0, xs);
  magic cast(pointer, binary, new-binary)
}

define append(t1: &binary, t2: &binary): binary {
  let p1 = Magic.cast(&binary, pointer, t1) in
  let p2 = Magic.cast(&binary, pointer, t2) in
  let from1 = Mem.load-int(p1) in
  let from2 = Mem.load-int(p2) in
  let u = Word.size in
  let u2 = mul-int(u, 2) in
  let to1 = Mem.load-int(shift(p1, u)) in
  let to2 = Mem.load-int(shift(p2, u)) in
  let len1 = sub-int(to1, from1) in
  let len2 = sub-int(to2, from2) in
  let new-len = add-int(len1, len2) in
  let content1 = shift(p1, u2) in
  let content2 = shift(p2, u2) in
  let new-binary = Ext.malloc(C-Size(add-int(new-len, u2))) in
  Mem.store-int(0, new-binary);
  Mem.store-int(new-len, shift(new-binary, u));
  let !store-values =
    define store-values(target-pointer: pointer, content-array: pointer, current-index: int, array-length: int): unit {
      if eq-int(current-index, array-length) {
        Unit
      } else {
        let char = Mem.load-int8(shift(content-array, current-index)) in
        Mem.store-int8(char, shift(target-pointer, current-index));
        store-values(target-pointer, content-array, add-int(current-index, 1), array-length)
      }
    }
  in
  let str-pointer = shift(new-binary, u2) in
  !store-values(str-pointer, shift(content1, from1), 0, len1);
  !store-values(shift(str-pointer, len1), shift(content2, from2), 0, len2);
  Magic.cast(pointer, binary, new-binary)
}

// O(1)
define drop(t: binary, offset: int): binary {
  let offset = max-int(0, offset) in
  let _ on t =
    let ptr = Magic.cast(&binary, pointer, t) in
    let from = Mem.load-int(ptr) in
    let to-ptr = shift(ptr, Word.size) in
    let to = Mem.load-int(to-ptr) in
    Mem.store-int(min-int(to, add-int(from, offset)), ptr)
  in
  t
}

// O(1)
define take(t: binary, offset: int): binary {
  let offset = max-int(0, offset) in
  let _ on t =
    let ptr = Magic.cast(&binary, pointer, t) in
    let from = Mem.load-int(ptr) in
    let to-ptr = shift(ptr, Word.size) in
    let to = Mem.load-int(to-ptr) in
    Mem.store-int(min-int(to, add-int(from, offset)), to-ptr)
  in
  t
}

// O(1)
define slice(t: binary, start: int, size: int): binary {
  take(drop(t, start), size)
}

define drop-N(t: &binary, offset: int): binary {
  let content-ptr = _get-content-pointer(t) in
  let orig-size = binary-length(t) in
  let new-size = max-int(0, sub-int(orig-size, offset)) in
  let new-ptr = Ext.malloc(C-Size(add-int(new-size, mul-int(Word.size, 2)))) in
  Mem.store-int(0, new-ptr);
  Mem.store-int(new-size, shift(new-ptr, Word.size));
  let new-content-ptr = shift(new-ptr, mul-int(Word.size, 2)) in
  let adjusted-offset = min-int(offset, orig-size) in
  let content-ptr' = shift(content-ptr, adjusted-offset) in
  _copy-binary(content-ptr', new-content-ptr, new-size);
  Magic.cast(pointer, binary, new-ptr)
}

define take-N(t: &binary, offset: int): binary {
  let ptr = core.magic.cast(&binary, pointer, t) in
  let to = Mem.load-int(shift(ptr, core.word.size)) in
  let new-size = min-int(to, offset) in
  let new-ptr = Ext.malloc(C-Size(add-int(offset, mul-int(core.word.size, 2)))) in
  Mem.store-int(0, new-ptr);
  Mem.store-int(new-size, shift(new-ptr, core.word.size));
  let content-ptr = _get-content-pointer(t) in
  let new-content-ptr = shift(new-ptr, mul-int(core.word.size, 2)) in
  _copy-binary(content-ptr, new-content-ptr, new-size);
  core.magic.cast(pointer, binary, new-ptr)
}

define slice-N(t: &binary, start: int, size: int): binary {
  let start = max-int(start, 0) in
  let size = max-int(size, 0) in
  let ptr = Magic.cast(&binary, pointer, t) in
  let content-ptr = _get-content-pointer(t) in
  let to-ptr = shift(ptr, Word.size) in
  let to = Mem.load-int(to-ptr) in
  let new-size = min-int(size, to) in
  let new-ptr = Ext.malloc(C-Size(add-int(new-size, mul-int(Word.size, 2)))) in
  Mem.store-int(0, new-ptr);
  Mem.store-int(new-size, shift(new-ptr, Word.size));
  let new-content-ptr = shift(new-ptr, mul-int(Word.size, 2)) in
  _copy-binary(shift(content-ptr, start), new-content-ptr, new-size);
  Magic.cast(pointer, binary, new-ptr)
}

define take-end(t: binary, offset: int): binary {
  let len on t = binary-length(t) in
  drop(t, sub-int(len, offset))
}

define take-end-N(t: &binary, offset: int): binary {
  let len = binary-length(t) in
  drop-N(t, sub-int(len, offset))
}

define drop-end(t: binary, offset: int): binary {
  let len on t = binary-length(t) in
  take(t, sub-int(len, offset))
}

define drop-end-N(t: &binary, offset: int): binary {
  let len = binary-length(t) in
  take-N(t, sub-int(len, offset))
}

define intercalate(xs: list(binary), sep: binary): binary {
  join(this.list.intercalate(xs, sep))
}

define intercalate-N(xs: list(&binary), sep: &binary): binary {
  join-N(this.list.intercalate(xs, sep))
}

define find(xs: &binary, !pred: (int8) -> bool): ?int {
  let len = binary-length(xs) in
  let f =
    define self(index: int): ?int {
      if ge-int(index, len) {
        none()
      } else {
        let ch = binary-access(xs, index) in
        if !pred(ch) {
          Right(index)
        } else {
          self(add-int(index, 1))
        }
      }
    }
  in
  f(0)
}

define find-end(xs: &binary, !pred: (int8) -> bool): ?int {
  let len = binary-length(xs) in
  let f =
    define self(xs: &binary, index: int): ?int {
      if lt-int(index, 0) {
        none()
      } else {
        let ch = binary-access(xs, index) in
        if !pred(ch) {
          Right(sub-int(len, add-int(index, 1)))
        } else {
          self(xs, sub-int(index, 1))
        }
      }
    }
  in
  f(xs, sub-int(len, 1))
}
