import {
  this.ansi {Blue, Yellow},
  this.bool {True, bool},
  this.doc {
    Group,
    _nested-comma-seq,
    ansi-bold,
    ansi-fg,
    ansi-reset,
    atom-N,
    atom-float,
    atom-int,
    atom-rune,
    break,
    doc,
    empty,
    join,
  },
  this.doc.show {show-doc},
  this.either {Left, Right, either},
  this.list {Cons, Nil, list},
  this.magic,
  this.memory {load-int, load-pointer},
  this.pair {Pair, pair},
  this.pointer {shift},
  this.text {text},
  this.text.io {print, print-line},
  this.type-tag {
    Algebraic,
    Binary,
    Enum,
    Float16,
    Float32,
    Float64,
    Int1,
    Int16,
    Int2,
    Int32,
    Int4,
    Int64,
    Int8,
    Noema,
    Null,
    Own,
    Pointer,
    Rune,
    Vector,
    Wrapper,
  },
  this.unit {Unit, unit},
  this.vector {vector},
  this.word,
}

inline-meta _meta-length<a>(xs: list(a)): int {
  match xs {
  | Nil =>
    0
  | Cons(_, ys) =>
    add-int(1, _meta-length(ys))
  }
}

define _literal-doc(d: doc): doc {
  join[
    ansi-fg(Yellow),
    d,
    ansi-reset,
  ]
}

inline-meta _join-text(t1: &text, t2: &text): &text {
  let tmp = magic text-uncons(t1);
  match tmp {
  | Left(_) =>
    t2
  | Right(Pair(r, rest)) =>
    magic text-cons(r, _join-text(rest, t2))
  }
}

nominal {
  define-meta vet-doc<a>(x: '&a): 'doc,
}

define _constructor-doc(x: &text): doc {
  join[
    ansi-fg(Blue),
    ansi-bold,
    atom-N(x),
    ansi-reset,
  ]
}

inline-meta _vet-vector<a>(xs: '&vector(a)): 'doc {
  quote {
    let prefix = join[_constructor-doc("Vector"), atom-N("[")];
    let suffix = atom-N("]");
    let len = this.vector.length(splice {xs});
    let content =
      this.vector.fold-right-N(splice {xs}, empty, function (x: &a, acc: doc, i: int): doc {
        if eq-int(add-int(i, 1), len) {
          join[splice {vet-doc(quote {x})}, acc]
        } else {
          join[splice {vet-doc(quote {x})}, atom-N(","), break(" "), acc]
        }
      });
    Group(_nested-comma-seq(
      prefix,
      content,
      suffix,
    ))
  }
}

inline _load-nth-element(data-arg-num: int, cursor: int, x: pointer): pointer {
  let u = this.word.size;
  let offset = mul-int(u, add-int(1, add-int(data-arg-num, cursor))); // x = (disc, data-args, cons-args)
  let elem-ptr = shift(x, offset);
  load-pointer(elem-ptr)
}

inline-meta _vet-adt-cons<a>(
  cons-arg-types: list(type),
  cursor: int,
  x: '&a,
): 'doc {
  match cons-arg-types {
  | Nil =>
    quote {empty}
  | Cons(t, Nil) =>
    let data-arg-num = _meta-length(magic get-data-args(a));
    letveil t = t;
    quote {
      let data-arg-num = splice {promote {data-arg-num}};
      let low-cursor = splice {promote {cursor}};
      let x-ptr = this.magic.cast<&a, pointer>(splice {x});
      let x-elem-ptr = this.magic.cast<pointer, &t>(_load-nth-element(data-arg-num, low-cursor, x-ptr));
      splice {vet-doc<t>(quote {x-elem-ptr})}
    }
  | Cons(t, rest) =>
    let d1 = {
      let data-arg-num = _meta-length(magic get-data-args(a));
      letveil t = t;
      quote {
        let data-arg-num = splice {promote {data-arg-num}};
        let low-cursor = splice {promote {cursor}};
        let x-ptr = this.magic.cast<&a, pointer>(splice {x});
        let x-elem-ptr = this.magic.cast<pointer, &t>(_load-nth-element(data-arg-num, low-cursor, x-ptr));
        splice {vet-doc<t>(quote {x-elem-ptr})}
      }
    };
    let d2 = _vet-adt-cons(rest, add-int(cursor, 1), x);
    quote {join[splice {d1}, atom-N(","), break(" "), splice {d2}]}
  }
}

inline-meta _vet-adt<a>(cons-index: int, cons-num: int, disc: 'int, x: '&a): 'doc {
  if eq-int(cons-index, cons-num) {
    quote {empty} // unreachable
  } else {
    let ts = magic get-constructor-arg-types(a, cons-index);
    quote {
      if eq-int(splice {promote {cons-index}}, splice {disc}) {
        splice {
          let cons-name = magic get-cons-name(a, cons-index);
          let is-const-like = magic get-cons-const-flag(a, cons-index);
          let prefix =
            if is-const-like {
              quote {_constructor-doc(splice {promote {cons-name}})}
            } else {
              quote {join[_constructor-doc(splice {promote {cons-name}}), atom-N("(")]}
            };
          let suffix =
            if is-const-like {
              quote {empty}
            } else {
              quote {atom-N(")")}
            };
          quote {
            Group(_nested-comma-seq(
              splice {prefix},
              splice {_vet-adt-cons(ts, 0, x)},
              splice {suffix},
            ))
          }
        }
      } else {
        splice {_vet-adt(add-int(cons-index, 1), cons-num, disc, x)}
      }
    }
  }
}

inline-meta _vet-enum<a>(cons-index: int, cons-num: int, disc: 'int, x: '&a): 'doc {
  if eq-int(cons-index, cons-num) {
    quote {empty} // unreachable
  } else {
    quote {
      if eq-int(splice {promote {cons-index}}, splice {disc}) {
        splice {
          let cons-name = magic get-cons-name(a, cons-index);
          quote {_constructor-doc(splice {promote {cons-name}})}
        }
      } else {
        splice {_vet-enum(add-int(cons-index, 1), cons-num, disc, x)}
      }
    }
  }
}

define-meta vet-doc<a>(x: '&a): 'doc {
  let k = magic get-type-tag(a);
  match k {
  | Algebraic =>
    let num = magic get-cons-size(a);
    quote {
      let ptr-x = this.magic.cast<&a, pointer>(splice {x});
      let discriminant-x = load-int(ptr-x);
      splice {_vet-adt(0, num, quote {discriminant-x}, x)}
    }
  | Enum =>
    let num = magic get-cons-size(a);
    quote {
      let discriminant-x = this.magic.cast<&a, int>(splice {x});
      splice {_vet-enum(0, num, quote {discriminant-x}, x)}
    }
  | Int1 =>
    quote {atom-int(magic cast(int1, int64, magic cast(&a, int1, splice {x})))}
  | Int2 =>
    quote {atom-int(magic cast(int2, int64, magic cast(&a, int2, splice {x})))}
  | Int4 =>
    quote {atom-int(magic cast(int4, int64, magic cast(&a, int4, splice {x})))}
  | Int8 =>
    quote {atom-int(magic cast(int8, int64, magic cast(&a, int8, splice {x})))}
  | Int16 =>
    quote {atom-int(magic cast(int16, int64, magic cast(&a, int16, splice {x})))}
  | Int32 =>
    quote {atom-int(magic cast(int32, int64, magic cast(&a, int32, splice {x})))}
  | Int64 =>
    quote {atom-int(magic cast(&a, int64, splice {x}))}
  | Float16 =>
    quote {atom-float(magic cast(float16, float64, magic cast(&a, float16, splice {x})), 6)}
  | Float32 =>
    quote {atom-float(magic cast(float32, float64, magic cast(&a, float32, splice {x})), 6)}
  | Float64 =>
    quote {atom-float(magic cast(&a, float64, splice {x}), 6)}
  | Null =>
    quote {atom-N("null")}
  | Rune =>
    quote {
      _literal-doc(join[
        atom-N("`"),
        atom-rune(magic cast(&a, rune, splice {x})),
        atom-N("`"),
      ])
    }
  | Binary =>
    quote {
      _literal-doc(join[
        atom-N("\""),
        atom-N(magic cast(&a, &text, splice {x})),
        atom-N("\""),
      ])
    }
  | Wrapper =>
    letveil t = magic get-wrapper-content-type(a);
    quote {
      let x-unwrapped: &t = this.magic.cast<&a, &t>(splice {x});
      splice {vet-doc<t>(quote {x-unwrapped})}
    }
  | Vector =>
    letveil t = magic get-vector-content-type(a);
    quote {
      let x: &vector(t) = this.magic.cast<&a, &vector(t)>(splice {x});
      splice {_vet-vector<t>(quote {x})}
    }
  | Noema =>
    letveil t = magic get-noema-content-type(a);
    quote {
      let x-unwrapped: &t = this.magic.cast<&a, &t>(splice {x});
      splice {vet-doc<t>(quote {x-unwrapped})}
    }
  | Own =>
    letveil t = magic get-box-content-type(a);
    quote {
      let x-unwrapped: &t = this.magic.cast<&a, &t>(splice {x});
      splice {vet-doc<t>(quote {x-unwrapped})}
    }
  | Pointer =>
    quote {atom-int(magic cast(&a, int64, splice {x}))}
  | _ =>
    quote {_literal-doc(atom-N("_"))}
  }
}

define-meta vet<a>[ribbon-length: 'int := quote {50}](x: '&a): 'unit {
  quote {
    pin tmp = splice {vet-doc(x)};
    pin item = show-doc(tmp, splice {ribbon-length});
    print(item);
    print("\n");
  }
}

define-meta trace<a>(x: 'a): 'a {
  introspect build-mode {
  | release =>
    x
  | default =>
    quote {
      let x = splice {x};
      let _ on x = splice {vet(quote {x})};
      x
    }
  }
}

define zen(): unit {
  pin xs: vector(pair(&text, &text)) = this.vector.Vector[Pair("a", "b"), Pair("c", "d")];
  // pin ys: vector(list(int)) = this.vector.Vector[List[], List[10, 20]];
  // let foo = splice {vet(quote {xs})};
  print-line("before");
  pin _: either(bool, list(float)) = Left(True);
  // Right(List[10, 208.2, 3.14, 20, 10, 20, 10]);
  // pin xs: vector(&text) = this.vector.Vector["foo", "bar", "buz", "hoge"];
  // pin xs: either(unit, list(int)) = Left(Unit);
  // run vet(xs);
  // vet[xs];
  // pin sample: meta text = box {*"test"};
  splice {vet(quote {xs})};
  print-line("after");
  // pin f: list((int) -> int) =
  //   List[function (x) {
  //     x
  //   }];
  // pin g: list((int) -> int) =
  //   List[function (x) {
  //     x
  //   }];
  // let foo = splice {vet(quote {f}, quote {g})};
  // print-bool-line(foo);
  Unit
}
