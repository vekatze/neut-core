import {
  this.binary {binary},
  this.binary.io,
  this.bool,
  this.either {Left, Right, right-unit},
  this.file.descriptor {descriptor, stdin, stdout},
  this.magic,
  this.system {get-error-message, report-system-error, system},
  this.text {_get-content-pointer, text, text-byte-length, to-binary},
  this.unit {Unit, unit},
}

foreign {
  neut_core_v0_51_write_line(int32, int, pointer): int,
}

define write<r := right-unit>(f: descriptor, t: &text): system(unit) {
  try _ = r;
  this.binary.io.write(f, to-binary(t))
}

define write-line<r := right-unit>(d: descriptor, t: &text): system(unit) {
  let d' = magic cast(descriptor, int32, d);
  let len = text-byte-length(t);
  let ptr = _get-content-pointer(t);
  let result = magic external neut_core_v0_51_write_line(d', len, ptr);
  if lt-int(result, 0) {
    try _ = r;
    report-system-error()
  } else {
    try _ = r;
    Right(Unit)
  }
}

define print(t: &text): unit {
  let _ = write(stdout, t);
  Unit
}

define print-line(t: &text): unit {
  let _ = write-line(stdout, t);
  Unit
}

define read<r := right-unit>(from: descriptor, initial-buffer-size: int): system(text) {
  try _ = r;
  try bin = this.binary.io.read(from, initial-buffer-size);
  Right(this.magic.cast(binary, text, bin))
}

define read-stdin<r := right-unit>(initial-buffer-size: int): system(text) {
  try _ = r;
  read(stdin, initial-buffer-size)
}

define zen(): unit {
  // print("hey\n");
  // Unit
  let _ = write(stdout, "hello.\n");
  let value = read(stdin, 10);
  match value {
  | Right(_) =>
    // pin txt = _Text(value);
    print("success\n")
    // print(txt)
  | Left(e) =>
    pin text = get-error-message(e);
    print(text);
    print("\n")
  }
}
