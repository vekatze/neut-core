import {
  this.c-size {C-Size},
  this.external {free, malloc},
  this.magic {cast},
  this.memory {store-int8},
  this.pointer {shift},
  this.text {_get-content-pointer, text, text-byte-length},
  this.type-tag {Opaque, type-tag-to-int},
  this.unit,
  this.word,
}

foreign {
  strlen(pointer): int,
  llvm.memcpy.p0.p0.i64(pointer, pointer, int64, int1): void,
  neut_core_v0_51_arch(): pointer,
}

nominal {
  c-string: type,
}

define _length-c-string(s: pointer): int {
  magic external strlen(s)
}

// null-terminated string
resource c-string {
  function (ptr: pointer) {
    free(ptr)
  },
  function (ptr: pointer) {
    let len = _length-c-string(ptr);
    let new-ptr = malloc(C-Size(add-int(len, 1)));
    let _ = magic external llvm.memcpy.p0.p0.i64(new-ptr, ptr, len, 0);
    new-ptr
  },
  type-tag-to-int(Opaque),
}

define length-c-string(s: &c-string): int {
  _length-c-string(magic cast(&c-string, pointer, s))
}

define to-c-string(t: &text): c-string {
  let len = text-byte-length(t);
  let new-ptr = malloc(C-Size(add-int(len, 1)));
  let src-ptr = _get-content-pointer(t);
  let _ = magic external llvm.memcpy.p0.p0.i64(new-ptr, src-ptr, len, 0);
  store-int8(0, shift(new-ptr, len));
  cast<pointer, c-string>(new-ptr)
}

define from-c-string(s: &c-string): text {
  let u = this.word.size;
  let u2 = mul-int(u, 2);
  let len = length-c-string(s);
  let new-ptr = malloc(C-Size(add-int(len, u2)));
  this.memory.store-int(0, new-ptr);
  this.memory.store-int(len, shift(new-ptr, u));
  let _ = magic external llvm.memcpy.p0.p0.i64(shift(new-ptr, u2), magic cast(&c-string, pointer, s), len, 0);
  cast<pointer, text>(new-ptr)
}
