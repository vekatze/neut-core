import {
  Ext,
  FD,
  Magic,
  Mem,
  Word,
  this.binary {_get-content-pointer, binary, binary-length},
  this.bool,
  this.c-size {C-Size, c-size, from-c-size, from-positive-ssize, lt-c-size},
  this.c-ssize {from-c-ssize, lt-c-ssize},
  this.either {Right},
  this.errno {report-system-error, system},
  this.file.descriptor {descriptor},
  this.pointer {extract, shift},
  this.unit {Unit, unit},
  this.word,
}

define write(b: &binary, f: descriptor): system(unit) {
  let content-pointer = _get-content-pointer(b) in
  let len = binary-length(b) in
  let loop =
    define write-loop(acc: int): system(unit) {
      let rest = sub-int(len, acc) in
      let head-ptr = shift(content-pointer, acc) in
      let bytes = core.external.write(f, head-ptr, C-Size(rest)) in
      if lt-c-ssize(bytes, 0) {
        report-system-error()
      } else {
        let acc = add-int(acc, from-c-ssize(bytes)) in
        if eq-int(acc, len) {
          Right(Unit)
        } else {
          write-loop(acc)
        }
      }
    }
  in
  loop(0)
}

define _read-inner(from: FD.descriptor, ptr: pointer, ptr-size: int, len-cand: int, buffer-size: c-size): system(binary) {
  let u = this.word.size in
  let content-size = sub-int(ptr-size, mul-int(u, 2)) in
  if lt-int(content-size, add-int(len-cand, from-c-size(buffer-size))) {
    let new-size = mul-int(ptr-size, 2) in
    let new-ptr = Ext.realloc(ptr, C-Size(new-size)) in
    if eq-int(extract(new-ptr), 0) {
      report-system-error()
    } else {
      _read-inner(from, new-ptr, new-size, len-cand, buffer-size)
    }
  } else {
    let content-ptr = shift(ptr, add-int(mul-int(u, 2), len-cand)) in
    let bytes = Ext.read(from, content-ptr, buffer-size) in
    let next-len-cand = add-int(len-cand, from-c-ssize(bytes)) in
    if lt-c-ssize(bytes, 0) {
      Ext.free(ptr);
      report-system-error()
    } else-if lt-c-size(from-positive-ssize(bytes), buffer-size) {
      Mem.store-int(next-len-cand, shift(ptr, u));
      Right(Magic.cast(pointer, binary, ptr))
    } else {
      _read-inner(from, ptr, ptr-size, next-len-cand, buffer-size)
    }
  }
}

define read(from: FD.descriptor, initial-buffer-size: int): system(binary) {
  let u = Word.size in
  let ptr-size = add-int(initial-buffer-size, mul-int(u, 2)) in
  let ptr = Ext.malloc(C-Size(ptr-size)) in
  Mem.store-int(0, ptr);
  _read-inner(from, ptr, ptr-size, 0, C-Size(initial-buffer-size))
}
