import {
  this.box,
  this.c-size {C-Size, from-c-size},
  this.either {Left, Right},
  this.external {
    free,
    malloc,
    thread-cond-destroy,
    thread-cond-init,
    thread-cond-signal,
    thread-cond-wait,
    thread-mutex-destroy,
    thread-mutex-init,
    thread-mutex-lock,
    thread-mutex-unlock,
  },
  this.foreign {THREAD_COND_SIZE, THREAD_MUTEX_SIZE},
  this.int {sum-int},
  this.list,
  this.magic {cast},
  this.memory {load-int, store-int},
  this.pair {Pair},
  this.pointer {shift},
  this.queue {dequeue, empty, enqueue, queue},
  this.type-tag {Opaque, type-tag-to-int},
  this.unit {unit},
  this.word,
}

inline _get-clone(a: type, x: &a): a {
  *x
}

nominal {
  _channel: type,
}

data channel(_: type) {
| _Channel(_channel)
}

define _new(a: type, q: queue(a)): channel(a) {
  let mutex-size = from-c-size(THREAD_MUTEX_SIZE);
  let cond-size = from-c-size(THREAD_COND_SIZE);
  let u = this.word.size;
  let ptr = malloc(C-Size(sum-int(List[u, mutex-size, cond-size, u])));
  store-int(cast(queue(a), int, q), ptr);
  let _ = thread-mutex-init(shift(ptr, u));
  thread-cond-init(shift(ptr, add-int(u, mutex-size)));
  store-int(cast(type, int, a), shift(ptr, add-int(u, add-int(mutex-size, cond-size))));
  cast(pointer, channel(a), ptr)
}

// (queue, thread-mutex, thread-cond)
resource _channel {
  function (ch: pointer) {
    let u = this.word.size;
    let mutex-size = from-c-size(THREAD_MUTEX_SIZE);
    let cond-size = from-c-size(THREAD_COND_SIZE);
    let cond-offset = add-int(u, mutex-size);
    let type-offset = add-int(u, add-int(mutex-size, cond-size));
    let a = cast(int, type, load-int(shift(ch, type-offset)));
    thread-cond-destroy(shift(ch, cond-offset));
    thread-mutex-destroy(shift(ch, u));
    let _: queue(a) = magic cast(int, queue(a), load-int(ch));
    free(ch)
  },
  function (ch: pointer) {
    let u = this.word.size;
    let mutex-size = from-c-size(THREAD_MUTEX_SIZE);
    let cond-size = from-c-size(THREAD_COND_SIZE);
    let type-offset = add-int(u, add-int(mutex-size, cond-size));
    let a = cast(int, type, load-int(shift(ch, type-offset)));
    let q: &queue(a) = cast(int, &queue(a), load-int(ch));
    let q-clone: queue(a) = _get-clone(queue(a), q);
    cast(channel(a), pointer, _new(a, q-clone))
  },
  type-tag-to-int(Opaque),
}

inline _make-channel<a>(): channel(a) {
  _new(a, empty)
}

define send<a>(ch: &channel(a), x: a): unit {
  let ch = cast(&channel(a), pointer, ch);
  let u = this.word.size;
  let mutex-ptr = shift(ch, u);
  thread-mutex-lock(mutex-ptr);
  let q-base: int = load-int(ch);
  let q: queue(a) = cast(int, queue(a), q-base);
  let x = x;
  let q = enqueue(q, x);
  store-int(cast(queue(a), int, q), ch);
  let mutex-size = from-c-size(THREAD_MUTEX_SIZE);
  thread-cond-signal(shift(ch, add-int(u, mutex-size)));
  thread-mutex-unlock(mutex-ptr)
}

define _receive-from-channel(a: type, ch: pointer, mutex-ptr: pointer): a {
  let q: queue(a) = cast(int, queue(a), load-int(ch));
  match dequeue(q) {
  | Right(Pair(v, q)) =>
    store-int(cast(queue(a), int, q), ch);
    thread-mutex-unlock(mutex-ptr);
    v
  | Left(_) =>
    store-int(cast(queue(int), int, empty), ch);
    let u = this.word.size;
    let mutex-size = from-c-size(THREAD_MUTEX_SIZE);
    let cond-ptr = shift(ch, add-int(u, mutex-size));
    thread-cond-wait(cond-ptr, mutex-ptr);
    _receive-from-channel(a, ch, mutex-ptr)
  }
}

define receive<a>(ch: &channel(a)): a {
  let ch = cast(&channel(a), pointer, ch);
  let u = this.word.size;
  let mutex-ptr = shift(ch, u);
  thread-mutex-lock(mutex-ptr);
  _receive-from-channel(a, ch, mutex-ptr)
}
