import {
  this.ansi {
    Background,
    Black,
    Blue,
    Bold,
    Color,
    Color-16,
    Color-256,
    Color-RGB,
    Cyan,
    Dull,
    Faint,
    Foreground,
    Green,
    Italic,
    Magenta,
    Normal,
    Red,
    Underline,
    Vivid,
    White,
    Yellow,
    color-ansi,
    style,
  },
  this.bool,
  this.box,
  this.doc.chunk {
    Chunk-Choose,
    Chunk-Empty,
    Chunk-Float,
    Chunk-Int,
    Chunk-Join,
    Chunk-Line,
    Chunk-Rune,
    Chunk-Style,
    Chunk-Style-N,
    Chunk-Text,
    Chunk-Text-N,
    _layout-chunk,
    _size-chunk,
    chunk,
  },
  this.doc.doc-kit {
    _Doc-Kit,
    _Horizontal,
    _Vertical,
    _doc-kit,
    _get-column,
    _get-indentation,
    _shift-indentation,
    _with-local-mode,
  },
  this.ref {_make-ref},
  this.text {text},
  this.text.io {print-line},
  this.unit {Unit, unit},
}

data doc {
| Chunk(chunk)
| Seq(doc, doc)
| Nest(int, doc)
| Group(doc)
}

inline empty: doc {
  Chunk(Chunk-Empty)
}

inline atom(t: text): doc {
  Chunk(Chunk-Text(t))
}

inline atom-N(t: &text): doc {
  Chunk(Chunk-Text-N(t))
}

inline atom-style(t: text): doc {
  Chunk(Chunk-Style(t))
}

inline atom-style-N(t: &text): doc {
  Chunk(Chunk-Style-N(t))
}

inline atom-int(x: int): doc {
  Chunk(Chunk-Int(x))
}

inline atom-float(x: float, decimals: int): doc {
  Chunk(Chunk-Float(x, decimals))
}

inline atom-rune(x: rune): doc {
  Chunk(Chunk-Rune(x))
}

inline break(t: &text): doc {
  Chunk(Chunk-Choose(Chunk-Text-N(t), Chunk-Line))
}

rule-right join {
  function leaf(_: int): doc {
    empty
  },
  function node-left(d: doc, acc: doc): doc {
    Seq(d, acc)
  },
  function root<a>(x: a): a {
    x
  },
}

inline newline: doc {
  Chunk(Chunk-Choose(Chunk-Text-N(""), Chunk-Line))
}

define _nested-comma-seq(prefix: doc, body: doc, suffix: doc): doc {
  join[
    prefix,
    Nest(
      2,
      join[
        break(""),
        Group(body),
      ],
    ),
    Chunk(Chunk-Choose(Chunk-Empty, Chunk-Join(Chunk-Text-N(","), Chunk-Line))),
    suffix,
  ]
}

define _size(target: &doc): int {
  case target {
  | Chunk(c) =>
    _size-chunk(c)
  | Seq(d1, d2) =>
    let s1 = _size(d1);
    let s2 = _size(d2);
    add-int(s1, s2)
  | Nest(_, cont) =>
    _size(cont)
  | Group(d) =>
    _size(d)
  }
}

inline ansi-doc(s: style): doc {
  match s {
  | Normal =>
    atom-style-N("\u{1b}[0m")
  | Bold =>
    atom-style-N("\u{1b}[1m")
  | Faint =>
    atom-style-N("\u{1b}[2m")
  | Italic =>
    atom-style-N("\u{1b}[3m")
  | Underline =>
    atom-style-N("\u{1b}[4m")
  | Color(color-layer, color) =>
    match color-layer {
    | Foreground =>
      match color {
      | Color-16(color-intensity, color) =>
        match color-intensity {
        | Dull =>
          match color {
          | Black =>
            atom-style-N("\u{1b}[30m")
          | Red =>
            atom-style-N("\u{1b}[31m")
          | Green =>
            atom-style-N("\u{1b}[32m")
          | Yellow =>
            atom-style-N("\u{1b}[33m")
          | Blue =>
            atom-style-N("\u{1b}[34m")
          | Magenta =>
            atom-style-N("\u{1b}[35m")
          | Cyan =>
            atom-style-N("\u{1b}[36m")
          | White =>
            atom-style-N("\u{1b}[37m")
          }
        | Vivid =>
          match color {
          | Black =>
            atom-style-N("\u{1b}[90m")
          | Red =>
            atom-style-N("\u{1b}[91m")
          | Green =>
            atom-style-N("\u{1b}[92m")
          | Yellow =>
            atom-style-N("\u{1b}[93m")
          | Blue =>
            atom-style-N("\u{1b}[94m")
          | Magenta =>
            atom-style-N("\u{1b}[95m")
          | Cyan =>
            atom-style-N("\u{1b}[96m")
          | White =>
            atom-style-N("\u{1b}[97m")
          }
        }
      | Color-256(value) =>
        join[
          atom-style-N("\u{1b}[38;5;"),
          atom-int(zext-int8-int(value)),
          atom-style-N("m"),
        ]
      | Color-RGB(r, g, b) =>
        join[
          atom-style-N("\u{1b}[38;2;"),
          atom-int(zext-int8-int(r)),
          atom-style-N(";"),
          atom-int(zext-int8-int(g)),
          atom-style-N(";"),
          atom-int(zext-int8-int(b)),
          atom-style-N("m"),
        ]
      }
    | Background =>
      match color {
      | Color-16(color-intensity, color) =>
        match color-intensity {
        | Dull =>
          match color {
          | Black =>
            atom-style-N("\u{1b}[40m")
          | Red =>
            atom-style-N("\u{1b}[41m")
          | Green =>
            atom-style-N("\u{1b}[42m")
          | Yellow =>
            atom-style-N("\u{1b}[43m")
          | Blue =>
            atom-style-N("\u{1b}[44m")
          | Magenta =>
            atom-style-N("\u{1b}[45m")
          | Cyan =>
            atom-style-N("\u{1b}[46m")
          | White =>
            atom-style-N("\u{1b}[47m")
          }
        | Vivid =>
          match color {
          | Black =>
            atom-style-N("\u{1b}[100m")
          | Red =>
            atom-style-N("\u{1b}[101m")
          | Green =>
            atom-style-N("\u{1b}[102m")
          | Yellow =>
            atom-style-N("\u{1b}[103m")
          | Blue =>
            atom-style-N("\u{1b}[104m")
          | Magenta =>
            atom-style-N("\u{1b}[105m")
          | Cyan =>
            atom-style-N("\u{1b}[106m")
          | White =>
            atom-style-N("\u{1b}[107m")
          }
        }
      | Color-256(value) =>
        join[
          atom-style-N("\u{1b}[48;5;"),
          atom-int(zext-int8-int(value)),
          atom-style-N("m"),
        ]
      | Color-RGB(r, g, b) =>
        join[
          atom-style-N("\u{1b}[48;2;"),
          atom-int(zext-int8-int(r)),
          atom-style-N(";"),
          atom-int(zext-int8-int(g)),
          atom-style-N(";"),
          atom-int(zext-int8-int(b)),
          atom-style-N("m"),
        ]
      }
    }
  }
}

define ansi-fg(c: color-ansi): doc {
  ansi-doc(Color(Foreground, Color-16(Vivid, c)))
}

define ansi-bg(c: color-ansi): doc {
  ansi-doc(Color(Background, Color-16(Vivid, c)))
}

define ansi-bold: doc {
  ansi-doc(Bold)
}

define ansi-reset: doc {
  ansi-doc(Normal)
}

define _layout(k: &_doc-kit, target: &doc): text {
  case target {
  | Chunk(c) =>
    _layout-chunk(k, c)
  | Seq(d1, d2) =>
    pin t1 = _layout(k, d1);
    pin t2 = _layout(k, d2);
    this.text.append(t1, t2)
  | Nest(i, cont) =>
    _shift-indentation(k, *i);
    let result = _layout(k, cont);
    _shift-indentation(k, mul-int(-1, *i));
    result
  | Group(d) =>
    let s = _size(d);
    tie _Doc-Kit of {width} = k;
    let column = _get-column(k);
    let indentation = _get-indentation(k);
    let ribbon-width = add-int(sub-int(column, indentation), s);
    let fits = lt-int(ribbon-width, *width);
    if fits {
      _with-local-mode(k, _Horizontal, function () {
        _layout(k, d)
      })
    } else {
      _with-local-mode(k, _Vertical, function () {
        _layout(k, d)
      })
    }
  }
}

define layout(max-ribbon-width: int, target: &doc): text {
  let column-ref = _make-ref(0);
  let indentation-ref = _make-ref(0);
  let mode-ref = _make-ref(_Horizontal);
  pin k = _Doc-Kit(max-ribbon-width, column-ref, indentation-ref, mode-ref);
  _layout(k, target)
}

define zen(): unit {
  pin item =
    Group(join[
      ansi-fg(Blue),
      atom(*"begin"),
      ansi-reset,
      Nest(2, join[
        break(" "),
        Group(
          join[
            ansi-fg(Yellow),
            atom(*"print;"),
            ansi-reset,
            break(" "),
            atom(*"test;"),
            break(" "),
            atom(*"stmt;"),
          ],
        ),
      ]),
      break(" "),
      ansi-fg(Blue),
      atom(*"end"),
      ansi-reset,
    ]);
  pin item = layout(10, item);
  print-line(item);
  Unit
}
